type PackageItem =
  | Fn of PACKAGE.Darklang.LanguageTools.ProgramTypes.PackageFn.PackageFn
  | Type of PACKAGE.Darklang.LanguageTools.ProgramTypes.PackageType
  | Constant of PACKAGE.Darklang.LanguageTools.ProgramTypes.PackageConstant

type Category =
  | Fn
  | Type
  | Constant

type GenericName =
  { owner: String
    modules: List<String>
    name: String
    version: Int }

type Entry =
  { name: GenericName
    package: PackageItem
    category: Category }

[<DB>]
type PackagesDB = Entry

[<HttpHandler("POST", "/types")>]
let _handler _req =
  let value =
    request.body
    |> PACKAGE.Darklang.Stdlib.String.fromBytesWithReplacement
    |> Builtin.Json.parse<PACKAGE.Darklang.LanguageTools.ProgramTypes.PackageType>
    |> Builtin.unwrap

  let item =
    Entry
      { name =
          GenericName
            { name =
                value.name.name
                |> PACKAGE.Darklang.PrettyPrinter.ProgramTypes.TypeName.name
              modules = value.name.modules
              owner = value.name.owner
              version = value.name.version }
        package = PackageItem.Type value
        category = Category.Type }

  let key = Builtin.DB.generateKey_v0 ()
  Builtin.DB.set item key PackagesDB

  PACKAGE.Darklang.Stdlib.Http.response
    (PACKAGE.Darklang.Stdlib.String.toBytes "added")
    200

[<HttpHandler("POST", "/functions")>]
let _handler _req =
  let value =
    request.body
    |> PACKAGE.Darklang.Stdlib.String.fromBytesWithReplacement
    |> Builtin.Json.parse<PACKAGE.Darklang.LanguageTools.ProgramTypes.PackageFn.PackageFn>
    |> Builtin.unwrap

  let item =
    Entry
      { name =
          GenericName
            { name =
                value.name.name
                |> PACKAGE.Darklang.PrettyPrinter.ProgramTypes.FnName.name
              modules = value.name.modules
              owner = value.name.owner
              version = value.name.version }
        package = PackageItem.Fn value
        category = Category.Fn }

  let key = Builtin.DB.generateKey_v0 ()
  Builtin.DB.set item key PackagesDB

  PACKAGE.Darklang.Stdlib.Http.response
    (PACKAGE.Darklang.Stdlib.String.toBytes "added")
    200

[<HttpHandler("POST", "/constants")>]
let _handler _req =
  let value =
    request.body
    |> PACKAGE.Darklang.Stdlib.String.fromBytesWithReplacement
    |> Builtin.Json.parse<PACKAGE.Darklang.LanguageTools.ProgramTypes.PackageConstant>
    |> Builtin.unwrap

  let item =
    Entry
      { name =
          GenericName
            { name =
                value.name.name
                |> PACKAGE.Darklang.PrettyPrinter.ProgramTypes.ConstantName.name
              modules = value.name.modules
              owner = value.name.owner
              version = value.name.version }
        package = PackageItem.Constant value
        category = Category.Constant }

  let key = Builtin.DB.generateKey_v0 ()
  Builtin.DB.set item key PackagesDB

  PACKAGE.Darklang.Stdlib.Http.response
    (PACKAGE.Darklang.Stdlib.String.toBytes "added")
    200



[<HttpHandler("GET", "/stats")>]
let _handler _req =
  let types =
    Builtin.DB.queryCount PackagesDB (fun entry -> entry.category == Category.Type)

  let fns =
    Builtin.DB.queryCount PackagesDB (fun entry -> entry.category == Category.Fn)

  let constants =
    Builtin.DB.queryCount PackagesDB (fun entry ->
      entry.category == Category.Constant)

  let items = Builtin.DB.count PackagesDB

  let body =
    $"Package stats:\ntypes: {Builtin.Int.toString types}, fns: {Builtin.Int.toString fns}, constants: {Builtin.Int.toString constants}\n\ntotal items in DB: {Builtin.Int.toString items}\n"
    |> PACKAGE.Darklang.Stdlib.String.toBytes

  PACKAGE.Darklang.Stdlib.Http.response body 200

let parseVersion (name: String) : (String * Int) =
  let parts = PACKAGE.Darklang.Stdlib.String.split name "_v"
  let name = (PACKAGE.Darklang.Stdlib.List.head parts) |> Builtin.unwrap

  let version =
    (PACKAGE.Darklang.Stdlib.List.last parts)
    |> Builtin.unwrap
    |> Builtin.Int.parse
    |> Builtin.unwrap

  (name, version)


let parseName (name: String) : GenericName =
  let parts = PACKAGE.Darklang.Stdlib.String.split name "."
  let owner = (PACKAGE.Darklang.Stdlib.List.head parts) |> Builtin.unwrap
  let name = (PACKAGE.Darklang.Stdlib.List.last parts) |> Builtin.unwrap

  let modules =
    parts
    |> PACKAGE.Darklang.Stdlib.List.tail
    |> Builtin.unwrap
    |> PACKAGE.Darklang.Stdlib.List.dropLast

  let (name, version) = parseVersion name

  GenericName
    { owner = owner
      modules = modules
      name = name
      version = version }

let fetchByName
  (name: String)
  (category: Category)
  : PACKAGE.Darklang.Stdlib.Http.Response =
  let name = parseName name

  let found =
    Builtin.DB.queryOne PackagesDB (fun v ->
      v.name == name && v.category == category)

  match found with
  | None ->
    PACKAGE.Darklang.Stdlib.Http.response
      (PACKAGE.Darklang.Stdlib.String.toBytes "not found")
      404
  | Some entry ->
    let json =
      match entry.package with
      | Fn fn ->
        Builtin.Json.serialize<PACKAGE.Darklang.LanguageTools.ProgramTypes.PackageFn.PackageFn>
          fn
      | Constant const' ->
        Builtin.Json.serialize<PACKAGE.Darklang.LanguageTools.ProgramTypes.PackageConstant>
          const'
      | Type typ ->
        Builtin.Json.serialize<PACKAGE.Darklang.LanguageTools.ProgramTypes.PackageType>
          typ

    let respBody = json |> Builtin.unwrap |> PACKAGE.Darklang.Stdlib.String.toBytes

    PACKAGE.Darklang.Stdlib.Http.response respBody 200



[<HttpHandler("GET", "/type/by-name/:name")>]
let _handler _req = fetchByName name Category.Type

[<HttpHandler("GET", "/constant/by-name/:name")>]
let _handler _req = fetchByName name Category.Constant

[<HttpHandler("GET", "/function/by-name/:name")>]
let _handler _req = fetchByName name Category.Fn

[<HttpHandler("GET", "/owner/:owner")>]
let _handler _req =
  let allItems =
    (Builtin.DB.query PackagesDB (fun v -> v.name.owner == owner))
    |> PACKAGE.Darklang.Stdlib.List.map (fun i ->
      (i.owner, i.modules |> String.join ".", i.name))
    |> PACKAGE.Darklang.Stdlib.List.map (fun m ->
      let (owner, modules, name) = m
      $"{owner}.{modules}.{name}")
    |> PACKAGE.Darklang.Stdlib.List.sort
    |> PACKAGE.Darklang.Stdlib.String.join "\n"

  let respBody =
    if allItems == "" then
      ($"owner {owner} not found") |> PACKAGE.Darklang.Stdlib.String.toBytes
    else
      allItems |> PACKAGE.Darklang.Stdlib.String.toBytes

  PACKAGE.Darklang.Stdlib.Http.response respBody 200


[<HttpHandler("GET", "/modules/:name")>]
let _handler _req =
  let owner =
    name
    |> PACKAGE.Darklang.Stdlib.String.split "."
    |> PACKAGE.Darklang.Stdlib.List.head
    |> Builtin.unwrap

  let modules =
    name
    |> PACKAGE.Darklang.Stdlib.String.split "."
    |> PACKAGE.Darklang.Stdlib.List.drop 1

  let fns =
    Builtin.DB.query PackageDB (fun v ->
      v.name.owner == owner
      && v.name.modules == modules
      && v.category == Category.Fn)

  let types =
    Builtin.DB.query PackageDB (fun v ->
      v.name.owner == owner
      && v.name.modules == modules
      && v.category == Category.Type)

  let constants =
    Builtin.DB.query PackageDB (fun v ->
      v.name.owner == owner
      && v.name.modules == modules
      && v.category == Category.Constant)

  let respBody =
    (PACKAGE.Darklang.Stdlib.Packages
      { types = types
        fns = fns
        constants = constants })
    |> Builtin.Json.serialize<PACKAGE.Darklang.Stdlib.Packages>
    |> Builtin.unwrap
    |> PACKAGE.Darklang.Stdlib.String.toBytes

  PACKAGE.Darklang.Stdlib.Http.response respBody 200