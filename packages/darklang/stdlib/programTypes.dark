module Darklang =
  module Stdlib =
    module ProgramTypes =

      /// Used to reference a type defined by a User, Standard Library module, or Package
      module FQTypeName =
        /// A type written in F# and shipped in the executable.
        /// Module required for all but a few cases.
        type StdlibTypeName =
          { modules: List<String>
            typ: String
            version: Int }

        /// A type in the package manager
        type PackageTypeName =
          { owner: String
            modules: List<String> // CLEANUP this is a nonemptylist internally
            typ: String
            version: Int }

        /// A type written by a User in a Canvas
        type UserTypeName =
          {
            /// The module in which the type is namespaced
            modules: List<String>

            /// The name of the user-defined type
            typ: String

            /// The version (0 or greater) of the type
            version: Int
          }

        type T =
          | User of PACKAGE.Darklang.Stdlib.ProgramTypes.FQTypeName.UserTypeName
          | Stdlib of PACKAGE.Darklang.Stdlib.ProgramTypes.FQTypeName.StdlibTypeName
          | Package of
            PACKAGE.Darklang.Stdlib.ProgramTypes.FQTypeName.PackageTypeName


      /// A Fully-Qualified Function Name
      /// Includes package, module, and version information where relevant.
      module FQFnName =
        /// Standard Library Function Name
        type StdlibFnName =
          { modules: List<String>
            function_: String
            version: Int }

        /// A Function written by a User in a Canvas
        type UserFnName =
          { modules: List<String>
            function_: String
            version: Int }

        /// A Function in the Package Manager
        type PackageFnName =
          {
            owner: String
            /// CLEANUP: was NonEmptyList<String> in F#
            modules: List<String>
            function_: String
            version: Int
          }

        type T =
          | User of PACKAGE.Darklang.Stdlib.ProgramTypes.FQFnName.UserFnName
          | Stdlib of PACKAGE.Darklang.Stdlib.ProgramTypes.FQFnName.StdlibFnName
          | Package of PACKAGE.Darklang.Stdlib.ProgramTypes.FQFnName.PackageFnName


      /// Darklang's available types (int, List<T>, user-defined types, etc.)
      type TypeReference =
        // A named variable, eg `a` in `List<a>`, matches anything
        | TVariable of String

        | TUnit
        | TBool
        | TInt
        | TFloat
        | TChar
        | TString
        | TDateTime
        | TUuid
        | TBytes
        | TPassword

        | TList of PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference

        | TTuple of
          PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference *
          PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference *
          List<PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference>

        | TDict of PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference

        /// A type defined by a standard library module, a canvas/user, or a package
        /// e.g. `Result<Int, String>` is represented as `TCustomType("Result", [TInt, TString])`
        /// `typeArgs` is the list of type arguments, if any
        | TCustomType of
          PACKAGE.Darklang.Stdlib.ProgramTypes.FQTypeName.T *
          typeArgs: List<PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference>

        | TDB of PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference

        | TFn of
          List<PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference> *
          PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference

        | TOption of PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference

        | TResult of
          PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference *
          PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference


      type LetPattern =
        | LPVariable of PACKAGE.Darklang.Stdlib.ID * name: String
        | LPTuple of
          PACKAGE.Darklang.Stdlib.ID *
          first: PACKAGE.Darklang.Stdlib.ProgramTypes.LetPattern *
          second: PACKAGE.Darklang.Stdlib.ProgramTypes.LetPattern *
          theRest: List<PACKAGE.Darklang.Stdlib.ProgramTypes.LetPattern>


      /// Used for pattern matching in a match statement
      type MatchPattern =
        | MPVariable of PACKAGE.Darklang.Stdlib.ID * String

        | MPUnit of PACKAGE.Darklang.Stdlib.ID
        | MPBool of PACKAGE.Darklang.Stdlib.ID * Bool
        | MPInt of PACKAGE.Darklang.Stdlib.ID * Int
        | MPFloat of
          PACKAGE.Darklang.Stdlib.ID *
          PACKAGE.Darklang.Stdlib.Sign *
          String *
          String
        | MPChar of PACKAGE.Darklang.Stdlib.ID * String
        | MPString of PACKAGE.Darklang.Stdlib.ID * String

        | MPList of
          PACKAGE.Darklang.Stdlib.ID *
          List<PACKAGE.Darklang.Stdlib.ProgramTypes.MatchPattern>
        | MPListCons of
          PACKAGE.Darklang.Stdlib.ID *
          head: PACKAGE.Darklang.Stdlib.ProgramTypes.MatchPattern *
          tail: PACKAGE.Darklang.Stdlib.ProgramTypes.MatchPattern

        | MPTuple of
          PACKAGE.Darklang.Stdlib.ID *
          PACKAGE.Darklang.Stdlib.ProgramTypes.MatchPattern *
          PACKAGE.Darklang.Stdlib.ProgramTypes.MatchPattern *
          List<PACKAGE.Darklang.Stdlib.ProgramTypes.MatchPattern>

        | MPEnum of
          PACKAGE.Darklang.Stdlib.ID *
          caseName: String *
          fieldPats: List<PACKAGE.Darklang.Stdlib.ProgramTypes.MatchPattern>


      type BinaryOperation =
        | BinOpAnd
        | BinOpOr

      type InfixFnName =
        | ArithmeticPlus
        | ArithmeticMinus
        | ArithmeticMultiply
        | ArithmeticDivide
        | ArithmeticModulo
        | ArithmeticPower
        | ComparisonGreaterThan
        | ComparisonGreaterThanOrEqual
        | ComparisonLessThan
        | ComparisonLessThanOrEqual
        | ComparisonEquals
        | ComparisonNotEquals
        | StringConcat

      type Infix =
        | InfixFnCall of PACKAGE.Darklang.Stdlib.ProgramTypes.InfixFnName
        | BinOp of PACKAGE.Darklang.Stdlib.ProgramTypes.BinaryOperation


      type StringSegment =
        | StringText of String
        | StringInterpolation of PACKAGE.Darklang.Stdlib.ProgramTypes.Expr


      type PipeExpr =
        | EPipeVariable of PACKAGE.Darklang.Stdlib.ID * String

        | EPipeLambda of
          PACKAGE.Darklang.Stdlib.ID *
          List<PACKAGE.Darklang.Stdlib.ID * String> *
          PACKAGE.Darklang.Stdlib.ProgramTypes.Expr

        | EPipeInfix of
          PACKAGE.Darklang.Stdlib.ID *
          PACKAGE.Darklang.Stdlib.ProgramTypes.Infix *
          PACKAGE.Darklang.Stdlib.ProgramTypes.Expr

        | EPipeFnCall of
          PACKAGE.Darklang.Stdlib.ID *
          PACKAGE.Darklang.Stdlib.ProgramTypes.FQFnName.T *
          typeArgs: List<PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference> *
          args: List<PACKAGE.Darklang.Stdlib.ProgramTypes.Expr>

        | EPipeEnum of
          PACKAGE.Darklang.Stdlib.ID *
          typeName: PACKAGE.Darklang.Stdlib.ProgramTypes.FQTypeName.T *
          caseName: String *
          fields: List<PACKAGE.Darklang.Stdlib.ProgramTypes.Expr>

      // module PipeExpr =
      //   let toID (expr : PipeExpr) : id =
      //     match expr with
      //     | EPipeVariable (id, _)
      //     | EPipeLambda (id, _, _)
      //     | EPipeInfix (id, _, _)
      //     | EPipeFnCall (id, _, _, _)
      //     | EPipeEnum (id, _, _, _) -> id


      /// Expressions - the main part of the language.
      type Expr =
        | EUnit of PACKAGE.Darklang.Stdlib.ID

        // simple data

        | EBool of PACKAGE.Darklang.Stdlib.ID * Bool

        | EInt of PACKAGE.Darklang.Stdlib.ID * Int

        // Allow the user to have arbitrarily big numbers, even if they don't make sense as
        // floats. The float is split as we want to preserve what the user entered.
        // Strings are used as numbers lose the leading zeros (eg 7.00007)
        | EFloat of
          PACKAGE.Darklang.Stdlib.ID *
          PACKAGE.Darklang.Stdlib.Sign *
          String *
          String

        /// A character is an Extended Grapheme Cluster (hence why we use a string). This
        /// is equivalent to one screen-visible "character" in Unicode.
        | EChar of PACKAGE.Darklang.Stdlib.ID * String

        | EString of
          PACKAGE.Darklang.Stdlib.ID *
          List<PACKAGE.Darklang.Stdlib.ProgramTypes.StringSegment>


        // structures of data

        | EList of
          PACKAGE.Darklang.Stdlib.ID *
          List<PACKAGE.Darklang.Stdlib.ProgramTypes.Expr>

        | EDict of
          PACKAGE.Darklang.Stdlib.ID *
          List<String * PACKAGE.Darklang.Stdlib.ProgramTypes.Expr>

        | ETuple of
          PACKAGE.Darklang.Stdlib.ID *
          PACKAGE.Darklang.Stdlib.ProgramTypes.Expr *
          PACKAGE.Darklang.Stdlib.ProgramTypes.Expr *
          List<Expr>

        | ERecord of
          PACKAGE.Darklang.Stdlib.ID *
          PACKAGE.Darklang.Stdlib.ProgramTypes.FQTypeName.T *
          List<String * PACKAGE.Darklang.Stdlib.ProgramTypes.Expr>

        // Enums include `Just`, `Nothing`, `Error`, `Ok`, as well
        // as user-defined enums.
        //
        /// Given an Enum type of:
        ///   `type MyEnum = A | B of int | C of int * (label: string) | D of MyEnum`
        /// , this is the expression
        ///   `C (1, "title")`
        /// represented as
        ///   `EEnum(Some UserType.MyEnum, "C", [EInt(1), EString("title")]`
        /// TODO: the UserTypeName should eventually be a non-optional FQTypeName.
        | EEnum of
          PACKAGE.Darklang.Stdlib.ID *
          typeName: PACKAGE.Darklang.Stdlib.ProgramTypes.FQTypeName.T *
          caseName: String *
          fields: List<PACKAGE.Darklang.Stdlib.ProgramTypes.Expr>


        // declaring and accessing variables

        | ELet of
          PACKAGE.Darklang.Stdlib.ID *
          PACKAGE.Darklang.Stdlib.ProgramTypes.LetPattern *
          PACKAGE.Darklang.Stdlib.ProgramTypes.Expr *
          PACKAGE.Darklang.Stdlib.ProgramTypes.Expr

        | EFieldAccess of
          PACKAGE.Darklang.Stdlib.ID *
          PACKAGE.Darklang.Stdlib.ProgramTypes.Expr *
          String

        | EVariable of PACKAGE.Darklang.Stdlib.ID * String


        // control flow

        | EIf of
          PACKAGE.Darklang.Stdlib.ID *
          PACKAGE.Darklang.Stdlib.ProgramTypes.Expr *
          PACKAGE.Darklang.Stdlib.ProgramTypes.Expr *
          PACKAGE.Darklang.Stdlib.ProgramTypes.Expr

        | EMatch of
          PACKAGE.Darklang.Stdlib.ID *
          arg: PACKAGE.Darklang.Stdlib.ProgramTypes.Expr *
          cases:
            List<PACKAGE.Darklang.Stdlib.ProgramTypes.MatchPattern *
            PACKAGE.Darklang.Stdlib.ProgramTypes.Expr>

        | EPipe of
          PACKAGE.Darklang.Stdlib.ID *
          PACKAGE.Darklang.Stdlib.ProgramTypes.Expr *
          PACKAGE.Darklang.Stdlib.ProgramTypes.PipeExpr *
          List<PACKAGE.Darklang.Stdlib.ProgramTypes.PipeExpr>


        // function calls

        | EInfix of
          PACKAGE.Darklang.Stdlib.ID *
          PACKAGE.Darklang.Stdlib.ProgramTypes.Infix *
          PACKAGE.Darklang.Stdlib.ProgramTypes.Expr *
          PACKAGE.Darklang.Stdlib.ProgramTypes.Expr

        | ELambda of
          PACKAGE.Darklang.Stdlib.ID *
          List<PACKAGE.Darklang.Stdlib.ID * String> *
          PACKAGE.Darklang.Stdlib.ProgramTypes.Expr

        | EFnCall of
          PACKAGE.Darklang.Stdlib.ID *
          PACKAGE.Darklang.Stdlib.ProgramTypes.FQFnName.T *
          typeArgs: List<PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference> *
          args: List<PACKAGE.Darklang.Stdlib.ProgramTypes.Expr>

        | ERecordUpdate of
          PACKAGE.Darklang.Stdlib.ID *
          record: PACKAGE.Darklang.Stdlib.ProgramTypes.Expr *
          updates: List<String * PACKAGE.Darklang.Stdlib.ProgramTypes.Expr>


      module Expr =
        let toID
          (expr: PACKAGE.Darklang.Stdlib.ProgramTypes.Expr)
          : PACKAGE.Darklang.Stdlib.ID =
          match expr with
          | EUnit id -> id
          | EBool(id, _) -> id
          | EInt(id, _) -> id
          | EFloat(id, _, _, _) -> id
          | EChar(id, _) -> id
          | EString(id, _) -> id
          | ELet(id, _, _, _) -> id
          | EIf(id, _, _, _) -> id
          | EInfix(id, _, _, _) -> id
          | ELambda(id, _, _) -> id
          | EFieldAccess(id, _, _) -> id
          | EVariable(id, _) -> id
          | EFnCall(id, _, _, _) -> id
          | EList(id, _) -> id
          | EDict(id, _) -> id
          | ETuple(id, _, _, _) -> id
          | EPipe(id, _, _, _) -> id
          | ERecord(id, _, _) -> id
          | EEnum(id, _, _, _) -> id
          | EMatch(id, _, _) -> id
          | ERecordUpdate(id, _, _) -> id


      // Used to mark whether a function/type has been deprecated, and if so,
      // details about possible replacements/alternatives, and reasoning
      type Deprecation<'name> =
        | NotDeprecated

        // The exact same thing is available under a new, preferred name
        | RenamedTo of 'name

        /// This has been deprecated and has a replacement we can suggest
        | ReplacedBy of 'name

        /// This has been deprecated and not replaced, provide a message for the user
        | DeprecatedBecause of String


      /// A stdlib-, user-, or package- defined type (an alias, record, or enum)
      module CustomType =
        type RecordField =
          { name: String
            typ: PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference
            description: String }

        type EnumField =
          { typ: PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference
            label: Option<String>
            description: String }

        type EnumCase =
          { name: String
            fields: List<PACKAGE.Darklang.Stdlib.ProgramTypes.CustomType.EnumField>
            description: String }

        type T =
          /// Alias/abbreviation of an existing type with an alternative name, to capture some meaning
          | Alias of PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference

          /// `type MyRecord = { a : int; b : string }`
          | Record of
            firstField: PACKAGE.Darklang.Stdlib.ProgramTypes.CustomType.RecordField *
            additionalFields:
              List<PACKAGE.Darklang.Stdlib.ProgramTypes.CustomType.RecordField>

          /// `type MyEnum = A | B of int | C of int * (label: string)`
          | Enum of
            firstCase: PACKAGE.Darklang.Stdlib.ProgramTypes.CustomType.EnumCase *
            additionalCases:
              List<PACKAGE.Darklang.Stdlib.ProgramTypes.CustomType.EnumCase>


      module Handler =
        type CronInterval =
          | EveryDay
          | EveryWeek
          | EveryFortnight
          | EveryHour
          | Every12Hours
          | EveryMinute

        type Spec =
          | HTTP of route: String * method: String
          | Worker of name: String
          | Cron of
            name: String *
            interval: PACKAGE.Darklang.Stdlib.ProgramTypes.Handler.CronInterval
          | REPL of name: String

        type T =
          { tlid: PACKAGE.Darklang.Stdlib.TLID
            ast: PACKAGE.Darklang.Stdlib.ProgramTypes.Expr
            spec: PACKAGE.Darklang.Stdlib.ProgramTypes.Handler.Spec }


      module DB =
        type T =
          { tlid: PACKAGE.Darklang.Stdlib.TLID
            name: String
            version: Int
            typ: PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference }


      /// A type that a User defined within a Canvas
      type UserType =
        {
          /// The toplevel identifier
          tlid: Int // TODO: tlid should probably be an alias of TInt

          /// Name
          name: PACKAGE.Darklang.Stdlib.ProgramTypes.FQTypeName.UserTypeName

          typeParams: List<String>

          /// The type definition (Alias, Record, Enum, etc.)
          definition: PACKAGE.Darklang.Stdlib.ProgramTypes.CustomType.T
        }


      module UserFunction =
        type Parameter =
          { name: String
            typ: PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference
            description: String }

        type T =
          { tlid: PACKAGE.Darklang.Stdlib.TLID
            name: PACKAGE.Darklang.Stdlib.ProgramTypes.FQFnName.UserFnName
            typeParams: List<String>
            parameters:
              List<PACKAGE.Darklang.Stdlib.ProgramTypes.UserFunction.Parameter>
            returnType: PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference
            description: String
            deprecated:
              PACKAGE.Darklang.Stdlib.ProgramTypes.Deprecation<PACKAGE.Darklang.Stdlib.ProgramTypes.FQFnName.T>
            body: PACKAGE.Darklang.Stdlib.ProgramTypes.Expr }


      // module Toplevel =
      //   type T =
      //     | TLHandler of Handler.T
      //     | TLDB of DB.T
      //     | TLFunction of UserFunction.T
      //     | TLType of UserType.T

      //   let toTLID (tl : T) : tlid =
      //     match tl with
      //     | TLHandler h -> h.tlid
      //     | TLDB db -> db.tlid
      //     | TLFunction f -> f.tlid
      //     | TLType t -> t.tlid


      module Secret =
        type T =
          { name: String
            value: String
            version: Int }


      module PackageType =
        type T =
          { tlid: PACKAGE.Darklang.Stdlib.TLID
            id: Uuid
            name: PACKAGE.Darklang.Stdlib.ProgramTypes.FQTypeName.PackageTypeName
            typeParams: List<String>
            definition: PACKAGE.Darklang.Stdlib.ProgramTypes.CustomType.T
            description: String
            deprecated:
              PACKAGE.Darklang.Stdlib.ProgramTypes.Deprecation<PACKAGE.Darklang.Stdlib.ProgramTypes.FQTypeName.T> }


      module PackageFn =
        type Parameter =
          { name: String
            typ: PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference
            description: String }

        type T =
          { tlid: PACKAGE.Darklang.Stdlib.TLID
            id: Uuid
            name: PACKAGE.Darklang.Stdlib.ProgramTypes.FQFnName.PackageFnName
            body: PACKAGE.Darklang.Stdlib.ProgramTypes.Expr
            typeParams: List<String>
            parameters:
              List<PACKAGE.Darklang.Stdlib.ProgramTypes.PackageFn.Parameter>
            returnType: PACKAGE.Darklang.Stdlib.ProgramTypes.TypeReference
            description: String
            deprecated:
              PACKAGE.Darklang.Stdlib.ProgramTypes.Deprecation<PACKAGE.Darklang.Stdlib.ProgramTypes.FQFnName.T> }