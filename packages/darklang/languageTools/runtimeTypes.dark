module Darklang =
  module LanguageTools =
    module RuntimeTypes =
      // <aliases>
      type Option = PACKAGE.Darklang.Stdlib.Option.Option
      // </aliases>

      module FQName =
        /// A name that is built into the runtime
        type BuiltIn<'name> =
          { modules: List<String>
            name: 'name
            version: Int }

        /// Part of the user's program (e.g. canvas or CLI)
        type UserProgram<'name> =
          { modules: List<String>
            name: 'name
            version: Int }

        type Package<'name> =
          { owner: String
            modules: List<String>
            name: 'name
            version: Int }

        type FQName<'name> =
          | BuiltIn of BuiltIn<'name>
          | UserProgram of UserProgram<'name>
          | Package of Package<'name>


      module TypeName =
        type Name = TypeName of String
        type TypeName = FQName.FQName<Name>
        type BuiltIn = FQName.BuiltIn<Name>
        type UserProgram = FQName.UserProgram<Name>
        type Package = FQName.Package<Name>


      module FnName =
        type Name = FnName of String
        type FnName = FQName.FQName<Name>
        type BuiltIn = FQName.BuiltIn<Name>
        type UserProgram = FQName.UserProgram<Name>
        type Package = FQName.Package<Name>


      /// A Fully-Qualified Constant Name
      /// Includes package, module, and version information where relevant.
      module ConstantName =
        type Name = ConstantName of String

        type ConstantName = FQName.FQName<Name>

        type BuiltIn = FQName.BuiltIn<Name>
        type UserProgram = FQName.UserProgram<Name>
        type Package = FQName.Package<Name>


      type NameResolution<'a> =
        PACKAGE.Darklang.Stdlib.Result.Result<'a, RuntimeError>


      type TypeReference =
        | TUnit
        | TBool
        | TInt
        | TFloat
        | TChar
        | TString
        | TUuid
        | TBytes
        | TDateTime
        | TList of TypeReference
        | TTuple of TypeReference * TypeReference * List<TypeReference>
        | TFn of List<TypeReference> * TypeReference
        | TDB of TypeReference
        | TVariable of String
        | TCustomType of
          NameResolution<TypeName.TypeName> *
          typeArgs: List<TypeReference>
        | TDict of TypeReference

      type Expr =
        | EInt of ID * Int
        | EBool of ID * Bool
        | EString of ID * List<StringSegment>
        | EChar of ID * String
        | EFloat of ID * Float
        | EUnit of ID
        | EConstant of ID * ConstantName.ConstantName
        | ELet of ID * LetPattern * Expr * Expr
        | EIf of
          ID *
          cond: Expr *
          thenExpr: Expr *
          elseExpr: Stdlib.Option.Option<Expr>
        | ELambda of ID * List<ID * String> * Expr
        | EFieldAccess of ID * Expr * String
        | EVariable of ID * String
        | EApply of ID * Expr * typeArgs: List<TypeReference> * args: List<Expr>
        | EFnName of ID * FnName.FnName
        | EList of ID * List<Expr>
        | ETuple of ID * Expr * Expr * List<Expr>
        | ERecord of ID * TypeName.TypeName * List<String * Expr>
        | ERecordUpdate of ID * record: Expr * updates: List<String * Expr>
        | EDict of ID * List<String * Expr>
        | EEnum of ID * TypeName.TypeName * caseName: String * fields: List<Expr>
        | EMatch of ID * Expr * List<MatchPattern * Expr>
        | EAnd of ID * Expr * Expr
        | EOr of ID * Expr * Expr
        | EError of ID * RuntimeError * List<Expr>

      type LetPattern =
        | LPVariable of ID * name: String
        | LPTuple of
          ID *
          first: LetPattern *
          second: LetPattern *
          theRest: List<LetPattern>

      type StringSegment =
        | StringText of String
        | StringInterpolation of Expr

      type MatchPattern =
        | MPVariable of ID * String
        | MPEnum of ID * caseName: String * fieldPatterns: List<MatchPattern>
        | MPInt of ID * Int
        | MPBool of ID * Bool
        | MPChar of ID * String
        | MPString of ID * String
        | MPFloat of ID * Float
        | MPUnit of ID
        | MPTuple of ID * MatchPattern * MatchPattern * List<MatchPattern>
        | MPList of ID * List<MatchPattern>
        | MPListCons of ID * head: MatchPattern * tail: MatchPattern

      type DvalMap = Dict<Dval.Dval>

      type LambdaImpl =
        { typeSymbolTable: TypeSymbolTable
          symtable: Symtable
          parameters: List<ID * String>
          body: Expr }

      type FnValImpl =
        | Lambda of LambdaImpl
        | NamedFn of FnName.FnName

      type RuntimeError = RuntimeError of Dval.Dval

      type KnownType =
        | KTUnit
        | KTBool
        | KTInt
        | KTFloat
        | KTChar
        | KTString
        | KTUuid
        | KTBytes
        | KTDateTime
        | KTList of ValueType
        | KTTuple of ValueType * ValueType * List<ValueType>
        | KTFn of List<ValueType> * ValueType

        | KTDB of ValueType
        | KTCustomType of TypeName.TypeName * typeArgs: List<ValueType>

        | KTDict of ValueType

      type ValueType =
        | Unknown
        | Known of KnownType


      module Dval =
        type Dval =
          | DUnit
          | DBool of Bool
          | DInt of Int
          | DFloat of Float
          | DChar of String
          | DString of String
          | DDateTime of DateTime
          | DBytes of Bytes
          | DUuid of Uuid

          | DList of ValueType * List<Dval>
          | DDict of ValueType * DvalMap
          | DTuple of Dval * Dval * List<Dval>

          | DFnVal of FnValImpl

          | DDB of String

          | DRecord of
            runtimeTypeName: TypeName.TypeName *
            sourceTypeName: TypeName.TypeName *
            typeArgs: List<ValueType> *
            fields: DvalMap

          | DEnum of
            runtimeTypeName: TypeName.TypeName *
            sourceTypeName: TypeName.TypeName *
            typeArgs: List<ValueType> *
            caseName: String *
            fields: List<Dval>

          | DError of DvalSource * RuntimeError

        let dvalValueTypeTODO = ValueType.Unknown

        let toValueType (dv: Dval) : ValueType =
          match dv with
          | DUnit -> ValueType.Known KnownType.KTUnit
          | DBool _ -> ValueType.Known KnownType.KTBool
          | DInt _ -> ValueType.Known KnownType.KTInt
          | DFloat _ -> ValueType.Known KnownType.KTFloat
          | DChar _ -> ValueType.Known KnownType.KTChar
          | DString _ -> ValueType.Known KnownType.KTString
          | DUuid _ -> ValueType.Known KnownType.KTUuid
          | DBytes _ -> ValueType.Known KnownType.KTBytes
          | DDateTime _ -> ValueType.Known KnownType.KTDateTime

          | DList(t, _) -> ValueType.Known(KnownType.KTList t)
          | DDict(t, _) -> ValueType.Known(KnownType.KTDict t)
          | DTuple(first, second, theRest) ->
            (KnownType.KTTuple(
              toValueType first,
              toValueType second,
              theRest
              |> PACKAGE.Darklang.Stdlib.List.map (fun item -> toValueType item)
            ))
            |> ValueType.Known

          | DRecord(typeName, _, typeArgs, fields) ->
            ValueType.Known(KnownType.KTCustomType(typeName, typeArgs))

          | DEnum(typeName, _, typeArgs, _caseName, _fields) ->
            ValueType.Known(KnownType.KTCustomType(typeName, typeArgs))

          | DFnVal fnImpl ->
            match fnImpl with
            | Lambda lambda ->
              let ps =
                PACKAGE.Darklang.Stdlib.List.map lambda.parameters (fun _ ->
                  ValueType.Unknown)

              ValueType.Known(KnownType.KTFn(ps, ValueType.Unknown))

            // VTTODO look up type, etc?
            | NamedFn _named -> dvalValueTypeTODO

          // CLEANUP follow up when DDB has a typeReference
          // or look up the type of the DB by name
          | DDB _ -> ValueType.Unknown

          // DError is being moved out of Dval
          | DError _ -> dvalValueTypeTODO

      /// our record of any variable bindings in scope
      ///
      /// i.e. within the execution of `x+y` in
      ///  `let x = 1; let y = 2; x + y`
      /// , we would have a Symtable of
      ///   `{ "x" => DInt 1; "y" => DInt 2 }`
      type Symtable = Dict<Dval.Dval>

      type TypeSymbolTable = Dict<TypeReference>


      // Record the source of an incomplete or error. Would be useful to add more
      // information later, such as the iteration count that led to this, or
      // something like a stack trace
      type DvalSource =
        // We do not have context to supply an identifier
        | SourceNone

        // Caused by an expression of `id` within the given `tlid`
        | SourceID of TLID * ID

      type BuiltInParam =
        { name: String
          typ: TypeReference
          blockArgs: List<String>
          description: String }

      type Param = { name: String; typ: TypeReference }