module Darklang =
  module LanguageTools =
    module RuntimeErrors =
      module NameResolution =
        type ErrorType =
          | NotFound
          | ExpectedEnumButNot
          | ExpectedRecordButNot
          | MissingEnumModuleName of caseName: String
          | InvalidPackageName

        type NameType =
          | Function
          | Type
          | Constant

        type Error =
          {
            errorType: ErrorType

            nameType: NameType

            /// The `.`-delimited name _parts_
            /// e.g. `Builtin.List.fakeFunction` is `["Builtin"; "List"; "fakeFunction"]`
            names: List<String>
          }

        let toSegments (e: Error) : ErrorOutput =
          match e.errorType with
          | NotFound ->
            let nameType =
              match e.nameType with
              | Function -> "function"
              | Type -> "type"
              | Constant -> "constant"

            ErrorOutput
              { summary =
                  [ ErrorSegment.ErrorSegment.String $"There is no {nameType} named "
                    ErrorSegment.ErrorSegment.InlineVarName(
                      PACKAGE.Darklang.Stdlib.String.join e.names "."
                    ) ]
                extraExplanation = []
                actual = []
                expected = [] }

          | MissingEnumModuleName caseName ->
            let name =
              let names = Builtin.listAppend e.names [ caseName ]
              ErrorSegment.ErrorSegment.VarName(Builtin.stringJoin names ".")

            let summary =
              [ ErrorSegment.ErrorSegment.String "Missing type name for enum case"
                ErrorSegment.ErrorSegment.String ": "
                ErrorSegment.ErrorSegment.String caseName ]

            ErrorOutput
              { summary = summary
                extraExplanation = []
                actual = [ name ]
                expected = [] }

          | InvalidPackageName ->
            let summary =
              [ ErrorSegment.ErrorSegment.String "Invalid package name "
                ErrorSegment.ErrorSegment.InlineVarName(
                  PACKAGE.Darklang.Stdlib.String.join e.names "."
                ) ]

            ErrorOutput
              { summary = summary
                extraExplanation = []
                actual = []
                expected = [] }

          | ExpectedRecordButNot ->
            let summary =
              [ ErrorSegment.ErrorSegment.String "Expected a record but "
                ErrorSegment.ErrorSegment.InlineVarName(
                  PACKAGE.Darklang.Stdlib.String.join e.names "."
                )
                ErrorSegment.ErrorSegment.String " is an enum" ]

            ErrorOutput
              { summary = summary
                extraExplanation = []
                actual = []
                expected = [] }

          | _ ->
            // RTETODO:
            // | ExpectedEnumButNot
            ErrorOutput
              { summary =
                  [ ErrorSegment.ErrorSegment.String
                      "RTETODO NameResolution.toSegments" ]
                extraExplanation = []
                actual = []
                expected = [] }