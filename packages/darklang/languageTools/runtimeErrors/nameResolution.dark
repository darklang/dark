module Darklang =
  module LanguageTools =
    module RuntimeErrors =
      module NameResolution =
        type ErrorType =
          | NotFound
          | ExpectedEnumButNot
          | ExpectedRecordButNot
          | MissingModuleName
          | InvalidPackageName

        type NameType =
          | Function
          | Type
          | Constant

        type Error =
          {
            errorType: ErrorType

            nameType: NameType

            /// The `.`-delimited name _parts_
            /// e.g. `Builtin.List.fakeFunction` is `["List"; "fakeFunction"]`
            names: List<String>
          }

        let toSegments (e: Error) : ErrorOutput =
          match e.errorType with
          | NotFound ->
            let nameType =
              match e.nameType with
              | Function -> "function"
              | Type -> "type"
              | Constant -> "constant"

            ErrorOutput
              { summary =
                  [ ErrorSegment.ErrorSegment.String $"There is no {nameType} named "
                    ErrorSegment.ErrorSegment.InlineVarName(Builtin.String.join e.names ".") ]
                extraExplanation = []
                actual = []
                expected = [] }

          | MissingModuleName ->
            let suffix =
              if e.names == [] then
                []
              else
                [ ErrorSegment.ErrorSegment.String ": "
                  ErrorSegment.ErrorSegment.VarName(Builtin.String.join e.names ".") ]

            let summary =
              Builtin.List.append
                [ ErrorSegment.ErrorSegment.String "Missing module name" ]
                suffix

            ErrorOutput
              { summary = summary
                extraExplanation = []
                actual = []
                expected = [] }

          | InvalidPackageName ->
            let summary =
              [ ErrorSegment.ErrorSegment.String "Invalid package name "
                ErrorSegment.ErrorSegment.InlineVarName(Builtin.String.join e.names ".") ]

            ErrorOutput
              { summary = summary
                extraExplanation = []
                actual = []
                expected = [] }

          | ExpectedRecordButNot ->
            let summary =
              [ ErrorSegment.ErrorSegment.String "Expected a record but "
                ErrorSegment.ErrorSegment.InlineVarName(Builtin.String.join e.names ".")
                ErrorSegment.ErrorSegment.String " is an enum" ]

            ErrorOutput
              { summary = summary
                extraExplanation = []
                actual = []
                expected = [] }

          | _ ->
            // RTETODO:
            // | ExpectedEnumButNot
            ErrorOutput
              { summary =
                  [ ErrorSegment.ErrorSegment.String
                      "RTETODO NameResolution.toSegments" ]
                extraExplanation = []
                actual = []
                expected = [] }