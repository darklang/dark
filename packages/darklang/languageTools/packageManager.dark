/// For interacting with the "dark matter",
/// where package items (types, values, fns, etc) are maintained.
/// i.e. the packages + manager
///
/// Of note, this is the Dev-time PackageManager, not the Runtime PackageManager.
module Darklang.LanguageTools.PackageManager


module Type =
  let find
    (branchId: Stdlib.Option.Option<Uuid>)
    (location: ProgramTypes.PackageLocation)
    : Stdlib.Option.Option<Uuid> =
    Builtin.pmFindType branchId location

  let get
    (id: Uuid)
    : Stdlib.Option.Option<ProgramTypes.PackageType.PackageType> =
    Builtin.pmGetType id


module Value =
  let find
    (branchId: Stdlib.Option.Option<Uuid>)
    (location: ProgramTypes.PackageLocation)
    : Stdlib.Option.Option<Uuid> =
    Builtin.pmFindValue branchId location

  let get
    (id: Uuid)
    : Stdlib.Option.Option<ProgramTypes.PackageValue.PackageValue> =
    Builtin.pmGetValue id


module Function =
  let find
    (branchId: Stdlib.Option.Option<Uuid>)
    (location: ProgramTypes.PackageLocation)
    : Stdlib.Option.Option<Uuid> =
    Builtin.pmFindFn branchId location

  let get (id: Uuid) : Stdlib.Option.Option<ProgramTypes.PackageFn.PackageFn> =
    Builtin.pmGetFn id


module Search =
  let search
    (branchId: Stdlib.Option.Option<Uuid>)
    (query: ProgramTypes.Search.SearchQuery)
    : ProgramTypes.Search.SearchResults =
    let searchResults = Builtin.pmSearch branchId query

    ProgramTypes.Search.SearchResults
      { submodules = [ searchResults.submodules ]
        types = searchResults.types
        values = searchResults.values
        fns = searchResults.fns }

// CLEANUP: update this to a value rather than a function, once we have support for value records
let pm () : ProgramTypes.PackageManager.PackageManager =
  ProgramTypes.PackageManager.PackageManager
    { findType =
        fun (branchId, location) ->
          PackageManager.Type.find branchId location
      findValue =
        fun (branchId, location) ->
          PackageManager.Value.find branchId location
      findFn =
        fun (branchId, location) ->
          PackageManager.Function.find branchId location
      getFn = PackageManager.Function.get
      getValue = PackageManager.Value.get
      getType = PackageManager.Type.get
      search =
        fun (branchId, query) -> PackageManager.Search.search branchId query }