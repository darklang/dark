module Darklang.LanguageTools.LspServer.Sync

/// Handles `dark/listInstances` requests from VS Code
let handleListInstancesRequest
  (state: LspState)
  (requestID: JsonRPC.RequestId)
  : LspState =
  let instances = SCM.Instances.list ()

  let instancesJson =
    instances
    |> Stdlib.List.map (fun instance ->
      Json.Object
        [ ("id", Json.String (Stdlib.Uuid.toString instance.id))
          ("name", Json.String instance.name)
          ("url", Json.String instance.url) ])
    |> Json.Array

  let responseJson =
    let requestID = Stdlib.Option.Option.Some requestID
    (JsonRPC.Response.Ok.make requestID instancesJson)
    |> Stdlib.AltJson.format

  logAndSendToClient responseJson

  state


/// Handles `dark/startSyncService` requests from VS Code
let handleStartSyncServiceRequest
  (state: LspState)
  (requestID: JsonRPC.RequestId)
  : LspState =
  let result =
    match Cli.SyncService.startInBackground () with
    | Ok () ->
      [ ("success", Json.Bool true)
        ("message", Json.String "Sync service started") ]
      |> Json.Object
    | Error errMsg ->
      [ ("success", Json.Bool false); ("message", Json.String errMsg) ]
      |> Json.Object

  let responseJson =
    let requestID = Stdlib.Option.Option.Some requestID
    (JsonRPC.Response.Ok.make requestID result)
    |> Stdlib.AltJson.format

  logAndSendToClient responseJson

  state


/// Handles `dark/stopSyncService` requests from VS Code
let handleStopSyncServiceRequest
  (state: LspState)
  (requestID: JsonRPC.RequestId)
  : LspState =
  let result =
    match Cli.SyncService.stop () with
    | Ok () ->
      [ ("success", Json.Bool true)
        ("message", Json.String "Sync service stopped") ]
      |> Json.Object
    | Error errMsg ->
      [ ("success", Json.Bool false); ("message", Json.String errMsg) ]
      |> Json.Object

  let responseJson =
    let requestID = Stdlib.Option.Option.Some requestID
    (JsonRPC.Response.Ok.make requestID result)
    |> Stdlib.AltJson.format

  logAndSendToClient responseJson

  state
