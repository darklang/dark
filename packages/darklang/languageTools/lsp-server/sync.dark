module Darklang.LanguageTools.LspServer.Sync

/// Handles `dark/sync` requests from VS Code
let handleSyncRequest
  (state: LspState)
  (requestID: JsonRPC.RequestId)
  (instanceName: String)
  : LspState =
  let result =
    match Darklang.SCM.Instances.getByName instanceName with
    | Some instance ->
      let result = Darklang.SCM.Sync.sync instance.id instance.url state.branchID

      match result with
      | Ok message ->
        [ ("success", Json.Bool true); ("message", Json.String message) ]
        |> Json.Object
      | Error errorMsg ->
        [ ("success", Json.Bool false); ("message", Json.String errorMsg) ]
        |> Json.Object
    | None ->
      [ ("success", Json.Bool false)
        ("message", Json.String $"Instance '{instanceName}' not found.") ]
      |> Json.Object

  let responseJson =
    let requestID = Stdlib.Option.Option.Some requestID
    (JsonRPC.Response.Ok.make requestID result)
    |> Stdlib.AltJson.format

  logAndSendToClient responseJson

  state


/// Handles `dark/listInstances` requests from VS Code
let handleListInstancesRequest
  (state: LspState)
  (requestID: JsonRPC.RequestId)
  : LspState =
  let instances = Darklang.SCM.Instances.list ()

  let instancesJson =
    instances
    |> Stdlib.List.map (fun instance ->
      Json.Object
        [ ("id", Json.String (Stdlib.Uuid.toString instance.id))
          ("name", Json.String instance.name)
          ("url", Json.String instance.url) ])
    |> Json.Array

  let responseJson =
    let requestID = Stdlib.Option.Option.Some requestID
    (JsonRPC.Response.Ok.make requestID instancesJson)
    |> Stdlib.AltJson.format

  logAndSendToClient responseJson

  state
