module Darklang.Cli.SCM.Log


let execute (state: AppState) (args: List<String>) : AppState =
  let branchId = state.currentBranchId

  let limit =
    match args with
    | [ limitStr ] ->
      match Stdlib.Int64.parse limitStr with
      | Ok n -> n
      | Error _ -> 20L
    | _ -> 20L

  let commits = SCM.PackageOps.getCommitsWithAncestors branchId limit

  let currentBranchIdStr = Stdlib.Uuid.toString branchId

  if Stdlib.List.isEmpty commits then
    Stdlib.printLine "No commits yet."
  else
    commits
    |> Stdlib.List.iter (fun c ->
      let id =
        match Stdlib.Dict.get c "id" with
        | Some i -> Stdlib.String.first i 8L
        | None -> "????????"

      let message =
        match Stdlib.Dict.get c "message" with
        | Some m -> m
        | None -> "(no message)"

      let createdAt =
        match Stdlib.Dict.get c "createdAt" with
        | Some d -> d
        | None -> "?"

      let opCount =
        match Stdlib.Dict.get c "opCount" with
        | Some o -> o
        | None -> "?"

      let commitBranchId =
        match Stdlib.Dict.get c "branchId" with
        | Some b -> b
        | None -> ""

      let branchName =
        match Stdlib.Dict.get c "branchName" with
        | Some b -> b
        | None -> ""

      let isAncestor = commitBranchId != currentBranchIdStr && commitBranchId != ""

      if isAncestor then
        Stdlib.printLine $"{Cli.Colors.dim}{id}  {createdAt}  ({opCount} ops)  [{branchName}]{Cli.Colors.reset}"
        Stdlib.printLine $"{Cli.Colors.dim}    {message}{Cli.Colors.reset}"
      else
        Stdlib.printLine $"{Cli.Colors.cyan}{id}{Cli.Colors.reset}  {createdAt}  ({opCount} ops)"
        Stdlib.printLine $"    {message}"

      Stdlib.printLine "")

  state


let complete (_state: AppState) (_args: List<String>) : List<Completion.CompletionItem> =
  []


let help (_state: AppState) : Unit =
  [ "Usage: log [limit]"
    "Show commit history for the current branch and its ancestors."
    "Commits from parent branches are shown dimmed with the branch name."
    ""
    "Arguments:"
    "  limit   Maximum number of commits to show (default: 20)"
    ""
    "Use 'show <commit-id>' to see details of a specific commit." ]
  |> Stdlib.printLines
