module Darklang.Cli.SCM.Branch


let execute (state: AppState) (args: List<String>) : AppState =
  match args with
  | [] | [ "list" ] ->
    let branches = SCM.Branch.list ()
    if Stdlib.List.isEmpty branches then
      Stdlib.printLine "No branches."
    else
      branches
      |> Stdlib.List.iter (fun b ->
        let marker =
          if b.id == state.currentBranchId then
            Colors.success "* "
          else
            "  "
        Stdlib.printLine $"{marker}{b.name}")
    state

  | [ "create"; name ] ->
    let newBranch = SCM.Branch.create name state.currentBranchId
    Stdlib.printLine (Colors.success $"Created and switched to branch '{name}'")
    { state with currentBranchId = newBranch.id; cachedBranchName = name }

  | [ "switch"; name ] ->
    match SCM.Branch.getByName name with
    | Some b ->
      Stdlib.printLine $"Switched to branch '{name}'"
      { state with currentBranchId = b.id; cachedBranchName = name }
    | None ->
      Stdlib.printLine (Colors.error $"Branch '{name}' not found.")
      state

  | [ "rename"; oldName; newName ] ->
    match SCM.Branch.getByName oldName with
    | Some b ->
      match SCM.Branch.rename b.id newName with
      | Ok _ -> Stdlib.printLine (Colors.success $"Renamed '{oldName}' to '{newName}'")
      | Error e -> Stdlib.printLine (Colors.error $"Rename failed: {e}")
      state
    | None ->
      Stdlib.printLine (Colors.error $"Branch '{oldName}' not found.")
      state

  | [ "delete"; name ] ->
    match SCM.Branch.getByName name with
    | Some b ->
      match SCM.Branch.delete b.id with
      | Ok _ ->
        Stdlib.printLine (Colors.success $"Deleted branch '{name}'")
        if b.id == state.currentBranchId then
          let mainId = SCM.Branch.mainBranchId
          Stdlib.printLine "Switched to main."
          { state with currentBranchId = mainId; cachedBranchName = "main" }
        else
          state
      | Error e ->
        Stdlib.printLine (Colors.error $"Delete failed: {e}")
        state
    | None ->
      Stdlib.printLine (Colors.error $"Branch '{name}' not found.")
      state

  | _ ->
    Stdlib.printLine "Usage: branch [list|create|switch|rename|delete] [args]"
    state


let complete (_state: AppState) (args: List<String>) : List<Completion.CompletionItem> =
  match args with
  | [] -> [ "list"; "create"; "switch"; "rename"; "delete" ] |> Stdlib.List.map Completion.simple
  | [ "switch"; _ ] ->
    let branches = SCM.Branch.list ()
    Stdlib.List.map branches (fun b -> Completion.simple b.name)
  | [ "delete"; _ ] ->
    let branches = SCM.Branch.list ()
    Stdlib.List.map branches (fun b -> Completion.simple b.name)
  | _ -> []


let help (_state: AppState) : Unit =
  [ "Usage: branch [subcommand] [args]"
    ""
    "Subcommands:"
    "  (none)                  List all branches (* = current)"
    "  list                    List all branches (* = current)"
    "  create <name>           Create branch from current, switch to it"
    "  switch <name>           Switch to existing branch"
    "  rename <old> <new>      Rename a branch"
    "  delete <name>           Delete a branch"
    ""
    "Note: branch context does not persist between CLI invocations."
    "In interactive mode, 'branch switch' works for the session."
    "For non-interactive use, pass --branch <name> as a global flag:"
    "  dark --branch feature status"
    "  dark --branch feature commit \"my changes\"" ]
  |> Stdlib.printLines
