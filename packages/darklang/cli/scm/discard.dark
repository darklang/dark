module Darklang.Cli.SCM.Discard


let execute (state: AppState) (args: List<String>) : AppState =
  let branchId = state.currentBranchId

  let autoConfirm =
    (Stdlib.List.member_v0 args "--yes") || (Stdlib.List.member_v0 args "-y")

  let summary = SCM.PackageOps.getWipSummary branchId

  if summary.total == 0L then
    Stdlib.printLine "Nothing to discard."
    state
  else
    Stdlib.printLine "Will discard:"

    if summary.types > 0L then
      Stdlib.printLine $"  {Builtin.int64ToString summary.types} type(s)"

    if summary.values > 0L then
      Stdlib.printLine $"  {Builtin.int64ToString summary.values} value(s)"

    if summary.fns > 0L then
      Stdlib.printLine $"  {Builtin.int64ToString summary.fns} function(s)"

    Stdlib.printLine ""

    let proceed =
      if autoConfirm then
        true
      else
        Stdlib.printLine (Colors.warning "This cannot be undone. Proceed? (y/n): ")
        let response = Builtin.stdinReadLine ()
        (response == "y") || (response == "Y")

    if proceed then
      match SCM.PackageOps.discard branchId with
      | Ok count ->
        Stdlib.printLine (Colors.success $"Discarded {Builtin.int64ToString count} ops.")
        state
      | Error e ->
        Stdlib.printLine (Colors.error $"Discard failed: {e}")
        state
    else
      Stdlib.printLine "Discard cancelled."
      state


let complete (_state: AppState) (_args: List<String>) : List<Completion.CompletionItem> =
  []


let help (_state: AppState) : Unit =
  [ "Usage: discard [--yes|-y]"
    "Discard all uncommitted (WIP) changes on the current branch."
    ""
    "This permanently deletes all uncommitted changes. Use with caution."
    ""
    "Flags:"
    "  --yes, -y   Skip confirmation prompt" ]
  |> Stdlib.printLines
