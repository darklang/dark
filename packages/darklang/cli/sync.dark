module Darklang.Cli.Sync


let execute (state: Cli.AppState) (args: List<String>) : Cli.AppState =
  match args with
  | [instanceName] ->
    // Sync with specified instance by name
    match Darklang.SCM.Instances.getByName instanceName with
    | Some instance ->
      Stdlib.printLine $"Syncing with {instance.name} ({instance.url})..."
      Stdlib.printLine ""

      let result =
        Darklang.SCM.Sync.sync instance.id instance.url state.currentBranchId

      match result with
      | Ok message ->
        Stdlib.printLine (Colors.success message)
        state

      | Error errorMsg ->
        Stdlib.printLine (Colors.error errorMsg)
        state

    | None ->
      Stdlib.printLine (Colors.error $"Instance not found: {instanceName}")
      Stdlib.printLine ""
      Stdlib.printLine "Use 'instance list' to see configured instances."
      Stdlib.printLine "Use 'instance add <name> <url>' to add a new instance."
      state

  | _ ->
    Stdlib.printLine (Colors.error "Invalid arguments")
    Stdlib.printLine ""
    help state
    state


let complete (_state: Cli.AppState) (args: List<String>) : List<String> =
  match args with
  | [] ->
    let instances = Darklang.SCM.Instances.list ()
    Stdlib.List.map instances (fun i -> i.name)

  | [partial] ->
    let instances = Darklang.SCM.Instances.list ()

    instances
    |> Stdlib.List.map (fun i -> i.name)
    |> Stdlib.List.filter (fun name -> Stdlib.String.startsWith name partial)

  | _ -> []


let help (_state: Cli.AppState) : Unit =
  [ "Usage:"
    "  sync <instance-name>  - Sync with a configured instance"
    ""
    "Examples:"
    "  sync local            - Sync with 'local' instance"
    "  sync production       - Sync with 'production' instance"
    ""
    "Before syncing, configure instances using:"
    "  instance add local http://dark-packages.dlio.localhost:11001"
    "  instance list" ]
  |> Stdlib.printLines
