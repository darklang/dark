module Darklang.Cli.Account

type Subcommand =
  | Show
  | Switch of name: String
  | List

let parseArgs (args: List<String>) : Stdlib.Result.Result<Subcommand, String> =
  match args with
  | [] -> Stdlib.Result.Result.Ok Subcommand.Show
  | ["list"] -> Stdlib.Result.Result.Ok Subcommand.List
  | ["switch"; name] -> Stdlib.Result.Result.Ok(Subcommand.Switch name)
  | ["switch"] -> Stdlib.Result.Result.Error "Missing account name. Usage: account switch <name>"
  | _ -> Stdlib.Result.Result.Error "Unknown subcommand. Use 'account', 'account list', or 'account switch <name>'"


let execute (state: Cli.AppState) (args: List<String>) : Cli.AppState =
  match parseArgs args with
  | Error msg ->
    Stdlib.printLine (Colors.error msg)
    state

  | Ok subcommand ->
    match subcommand with
    | Show ->
      Stdlib.printLine $"Current account: {state.accountName}"
      state

    | List ->
      let accounts = Builtin.pmListAccounts ()
      Stdlib.printLine "Available accounts:"
      accounts
      |> Stdlib.List.iter (fun name ->
        let marker =
          if name == state.accountName then
            " (current)"
          else
            ""
        Stdlib.printLine $"  {name}{marker}")
      state

    | Switch name ->
      match Builtin.pmGetAccountByName name with
      | None ->
        Stdlib.printLine (Colors.error $"Account not found: {name}")
        state
      | Some uuid ->
        // Clear caches so queries use the new account's visibility rules
        Builtin.pmClearAllCaches ()
        Stdlib.printLine $"Switched to account: {name}"
        { state with accountName = name; accountID = Stdlib.Option.Option.Some uuid }


let complete (_state: Cli.AppState) (args: List<String>) : List<Cli.Completion.CompletionItem> =
  match args with
  | [] ->
    ["list"; "switch"]
    |> Stdlib.List.map Cli.Completion.simple
  | ["switch"] ->
    (Builtin.pmListAccounts ())
    |> Stdlib.List.map Cli.Completion.simple
  | ["switch"; partial] ->
    (Builtin.pmListAccounts ())
    |> Stdlib.List.filter (fun name -> Stdlib.String.startsWith name partial)
    |> Stdlib.List.map Cli.Completion.simple
  | _ -> []


let help (_state: Cli.AppState) : Cli.AppState =
  [ "Usage:"
    "  account                    - Show current account"
    "  account list               - List available accounts"
    "  account switch <name>      - Switch to a different account"
    ""
    "Examples:"
    "  account"
    "  account list"
    "  account switch Stachu"
    ""
    "Note: Account can also be set via DARK_ACCOUNT environment variable." ]
  |> Stdlib.printLines

  _state
