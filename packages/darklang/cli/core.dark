module Darklang.Cli

type Page =
  | MainPrompt
  | Experiments
  | InteractiveNav of Packages.Nav.Interactive.State

type AppState =
  { isExiting: Bool
    mainPrompt: String
    cursorPosition: Int64
    needsFullRedraw: Bool
    packageData: Packages.State
    currentPage: Page
    commandHistory: List<String>
    historyIndex: Int64
    savedPrompt: String

    currentBranchId: Stdlib.Option.Option<Uuid> }

let initState () : AppState =
  // Default to no branch selected (user can select one with 'branch switch')
  AppState
    { isExiting = false
      mainPrompt = ""
      cursorPosition = 0L
      needsFullRedraw = true
      packageData = Packages.initState()
      currentPage = Page.MainPrompt  // Default to main prompt page
      commandHistory = []
      historyIndex = -1L
      savedPrompt = ""

      currentBranchId = Stdlib.Option.Option.None }

// Helper to update core state fields while preserving package navigation fields
let updateCoreFields (state: AppState) (isExiting: Bool) (mainPrompt: String) (needsFullRedraw: Bool) : AppState =
  { state with
      isExiting = isExiting
      mainPrompt = mainPrompt
      needsFullRedraw = needsFullRedraw }

type Msg =
  | ProcessInput of String
  | KeyPressed of key: Stdlib.Cli.Stdin.Key.Key * modifiers: Stdlib.Cli.Stdin.Modifiers.Modifiers * keyChar: Stdlib.Option.Option<String>
  | Exit


module ExecutionError =
  // TODO migrate this to some ParseAndExecuteScript submodule
  type ExecutionError =
    { msg: String; metadata: Dict<String> }

  let toString (err: ExecutionError) : String =
    let metadataStr =
      err.metadata
      |> Stdlib.Dict.toList
      |> Stdlib.List.map (fun (k, v) -> $"- {k}: {v}")
      |> Stdlib.String.join "\n"
    $"Error: {err.msg}\n{metadataStr}"


// TODO: this Completion module could be extracted to its own file (utils/completion.dark)
// to reduce the size of core.dark
module Completion =
  type ParsedInput =
    { commandName: String
      args: List<String>
      isCompletingCommand: Bool }


  let parseInput (input: String) : ParsedInput =
    let trimmed = Stdlib.String.trim input

    if Stdlib.String.isEmpty trimmed then
      ParsedInput { commandName = ""; args = []; isCompletingCommand = true }
    else
      let words = Stdlib.String.split trimmed " "
      match words with
      | [] -> ParsedInput { commandName = ""; args = []; isCompletingCommand = true }
      | [command] -> ParsedInput { commandName = command; args = []; isCompletingCommand = true }
      | command :: args -> ParsedInput { commandName = command; args = args; isCompletingCommand = false }


  let getPartialCompletion (parsed: ParsedInput) : String =
    if parsed.isCompletingCommand then
      parsed.commandName
    else
      match Stdlib.List.last parsed.args with
      | Some lastArg -> lastArg
      | None -> ""


  let buildCompletedCommand (parsed: ParsedInput) (completion: String) : String =
    if parsed.isCompletingCommand then
      completion
    else
      // Replace last argument with completion
      let argsWithoutLast = Stdlib.List.dropLast parsed.args
      let newArgs = Stdlib.List.append argsWithoutLast [completion]
      let fullCommand = Stdlib.List.append [parsed.commandName] newArgs
      Stdlib.String.join fullCommand " "


  let getHintFromCompletions (partial: String) (completions: List<String>) : String =
    match completions with
    | [] -> ""
    | [singleCompletion] ->
      if Stdlib.String.startsWith singleCompletion partial then
        Stdlib.String.dropFirst singleCompletion (Stdlib.String.length partial)
      else
        ""
    | multiple ->
      let commonPrefix = findCommonPrefix multiple
      if Stdlib.String.startsWith commonPrefix partial && Stdlib.String.length commonPrefix > Stdlib.String.length partial then
        Stdlib.String.dropFirst commonPrefix (Stdlib.String.length partial)
      else
        ""


  // Standard completion for commands that complete with other command names
  // (right now, just `help`)
  let commandNamesCompletion (_state: AppState) (args: List<String>) : List<String> =
    match args with
    | [] ->
      // Suggest all command names and aliases
      let commands = Registry.allCommands ()
      let commandNames = Stdlib.List.map commands (fun cmd -> cmd.name)
      let allAliases = Stdlib.List.fold commands [] (fun acc cmd -> Stdlib.List.append acc cmd.aliases)
      Stdlib.List.append commandNames allAliases
    | [partialArg] ->
      // Filter by partial match
      let commands = Registry.allCommands ()
      let commandNames = Stdlib.List.map commands (fun cmd -> cmd.name)
      let allAliases = Stdlib.List.fold commands [] (fun acc cmd -> Stdlib.List.append acc cmd.aliases)
      let allOptions = Stdlib.List.append commandNames allAliases
      Stdlib.List.filter allOptions (fun name -> Stdlib.String.startsWith name partialArg)
    | _ ->
      // Only complete first argument
      []


  // Helper functions for finding common prefixes in completions
  let findCommonLength (str1: String) (str2: String) (index: Int64) (maxLen: Int64) : Int64 =
    if index >= maxLen then
      index
    else
      let char1 = Stdlib.String.slice str1 index (index + 1L)
      let char2 = Stdlib.String.slice str2 index (index + 1L)
      if char1 == char2 then
        findCommonLength str1 str2 (index + 1L) maxLen
      else
        index

  let findCommonPrefix (strings: List<String>) : String =
    match strings with
    | [] -> ""
    | [single] -> single
    | first :: rest ->
      Stdlib.List.fold rest first (fun acc next ->
        let maxLen = Stdlib.Int64.min (Stdlib.String.length acc) (Stdlib.String.length next)
        let commonLen = findCommonLength acc next 0L maxLen
        Stdlib.String.slice acc 0L commonLen)


module View =
  let formatError (message: String) : String =
    Colors.error ("Error: " ++ message)

  let formatSuccess (message: String) : String =
    Colors.success message


  let promptBranchPreamble (state: AppState): String =
    let currentBranch =
      match state.currentBranchId with
      | Some branchID -> Darklang.SCM.Branch.get branchID
      | None -> Stdlib.Option.Option.None

    match currentBranch with
    | Some branch -> $"[{branch.name}]"
    | None -> ""


  let formatPromptWithInput (state: AppState) : String =
    let branchStr = Colors.info (promptBranchPreamble state)

    let locationStr =
      let pathStr = Packages.formatLocation state.packageData.currentLocation
      if Stdlib.String.isEmpty pathStr then
        ""
      else
        Colors.dimText $" {pathStr}"

    let hint = Registry.getCompletionHint state state.mainPrompt
    if Stdlib.String.isEmpty hint then
      branchStr ++ locationStr ++ "> " ++ state.mainPrompt
    else
      branchStr ++ locationStr ++ "> " ++ state.mainPrompt ++ (Colors.hint hint)


  let formatWelcome () : String =
    Registry.getCompactCommandList ()



module Registry =
  type CommandHandler =
    { name: String
      description: String
      aliases: List<String>
      execute: AppState -> List<String> -> AppState
      help: AppState -> AppState // CLEANUP we shouldn't need to return an AppState? Like a string would be fine...
      complete: AppState -> List<String> -> List<String> }

  // TODO: this command list is very long and repetitive - consider:
  //   - auto-discovering commands via module introspection
  //   - using a declarative format (JSON/config file)
  //   - grouping related commands into sub-registries
  let allCommands () : List<CommandHandler> =
    [
      ("quit", "Exit the CLI", ["exit"; "q"], Quit.execute, Quit.help, Quit.complete)
      ("help", "Show help for commands", ["commands"; "?"], Help.execute, Help.help, Help.complete)
      ("branch", "Manage development branches", [], Packages.Branch.execute, Packages.Branch.help, Packages.Branch.complete)
      ("instance", "Manage remote instances for syncing", [], Instances.execute, Instances.help, Instances.complete)
      ("sync", "Sync package ops with remote instance", [], Sync.execute, Sync.help, Sync.complete)
      ("install", "Install CLI globally", [], Installation.Install.execute, Installation.Install.help, Installation.Install.complete)
      ("update", "Update CLI to latest version", ["upgrade"], Installation.Update.execute, Installation.Update.help, Installation.Update.complete)
      ("uninstall", "Remove CLI installation", [], Installation.Uninstall.execute, Installation.Uninstall.help, Installation.Uninstall.complete)
      ("status", "Show installation status", [], Installation.Status.execute, Installation.Status.help, Installation.Status.complete)
      ("version", "Display CLI version", ["--version"; "-v"], Installation.Version.execute, Installation.Version.help, Installation.Version.complete)
      ("nav", "Navigate between packages, modules, and entities", ["cd"], Packages.Nav.execute, Packages.Nav.help, Packages.Nav.complete)
      ("ls", "List package contents", ["dir"], Packages.Listing.execute, Packages.Listing.help, Packages.Listing.complete)
      //("pwd", "Show current package path", [], Packages.Navigation.executePWD, Packages.Navigation.pwdHelp, Packages.Navigation.pwdComplete)
      ("back", "Go back to previous location", [], Packages.Back.execute, Packages.Back.help, Packages.Back.complete)
      ("clear", "Clear the screen", ["cls"], Cli.Clear.execute, Cli.Clear.help, Cli.Clear.complete)
      ("run", "Run a function or script", [], Cli.Run.execute, Cli.Run.help, Cli.Run.complete)
      ("eval", "Evaluate a Dark expression", [], Cli.Eval.execute, Cli.Eval.help, Cli.Eval.complete)
      ("scripts", "Store, manage, and run Dark scripts", [], Cli.Scripts.execute, Cli.Scripts.help, Cli.Scripts.complete)
      ("view", "View details of a module, type, value, or fns", [], Packages.View.execute, Packages.View.help, Packages.View.complete)
      ("tree", "Display package hierarchy in tree format", [], Packages.Tree.execute, Packages.Tree.help, Packages.Tree.complete)
      ("search", "Search for packages, types, functions, and values", [], Packages.Search.execute, Packages.Search.help, Packages.Search.complete)
      ("val", "Create a new value", [], Packages.Create.Value.execute, Packages.Create.Value.help, Packages.Create.Value.complete)
      ("let", "Create a new value (alias for val)", [], Packages.Create.Value.execute, Packages.Create.Value.help, Packages.Create.Value.complete)
      ("fn", "Create a new function", ["function"], Packages.Create.Fn.execute, Packages.Create.Fn.help, Packages.Create.Fn.complete)
      ("type", "Create a new type", [], Packages.Create.Type.execute, Packages.Create.Type.help, Packages.Create.Type.complete)
      ("rm", "Remove a package item", [], Packages.Remove.execute, Packages.Remove.help, Packages.Remove.complete)
      ("experiments", "Try out various WIP CLI experiments", [], Cli.Experiments.execute, Cli.Experiments.help, Cli.Experiments.complete)
    ]
    |> Stdlib.List.map(
      // CLEANUP nitpicky: swap help and complete
      fun (name, desc, aliases, execute, help, complete) ->
        CommandHandler
          { name = name
            description = desc
            aliases = aliases
            execute = execute
            help = help
            complete = complete }
      )


  let findCommand (name: String) : CommandHandler =
    let commands = allCommands ()

    let allMatches = // CLEANUP use a Set?
      let nameMatches =
        commands
        |> Stdlib.List.filter  (fun cmd -> cmd.name == name)

      let aliasMatches =
        commands
        |> Stdlib.List.filter (fun cmd ->
          match Stdlib.List.findFirst cmd.aliases (fun alias -> alias == name) with
          | Some _ -> true
          | None -> false)

      Stdlib.List.append nameMatches aliasMatches

    match allMatches with
    | [handler] -> handler
    | handler :: _ -> handler // CLEANUP?
    | [] ->
      CommandHandler
        { name = name
          description = "Unknown command"
          aliases = []
          execute = fun state args ->
            Stdlib.printLine (View.formatError $"Unknown command: {name}")
            Stdlib.printLine "Use 'help' to see available commands."
            state
          help = fun state ->
            Stdlib.printLine $"No help available for command: {name}"
            state
          complete = fun state args -> [] }

  let executeCommand (name: String) (state: AppState) (args: List<String>) : AppState =
    let handler = findCommand name
    let ex = handler.execute
    ex state args

  let executeCommandHelp (name: String) (state: AppState) : AppState =
    let handler = findCommand name
    let help = handler.help
    let resultState = help state

    // Automatically append alias information if aliases exist
    match handler.aliases with
    | [] -> resultState
    | aliases ->
      let aliasText = Stdlib.String.join aliases ", "
      Stdlib.printLine ""
      Stdlib.printLine $"Aliases: {aliasText}"
      resultState

  // TODO: formatCommandGroup + getCommandList logic could be simplified or moved to View module
  // Helper function to format a group of commands with details
  let formatCommandGroup (groupName: String) (commandNames: List<String>) (commands: List<CommandHandler>) : String =
    let formattedCommands =
      commandNames
      |> Stdlib.List.filterMap (fun name ->
        match Stdlib.List.findFirst commands (fun cmd -> cmd.name == name) with
        | Some cmd ->
          match cmd.aliases with
          | [] -> Stdlib.Option.Option.Some $"  {cmd.name} - {cmd.description}"
          | aliases ->
            let aliasText = Stdlib.String.join aliases ", "
            Stdlib.Option.Option.Some $"  {cmd.name} ({aliasText}) - {cmd.description}"
        | None -> Stdlib.Option.Option.None)
    let commandLines = Stdlib.String.join formattedCommands "\n"
    $"{groupName}:\n{commandLines}"

  // Helper function to format a group of commands compactly (just names)
  let formatCompactGroup (groupName: String) (cmdNames: List<String>) : String =
    let namesList = Stdlib.String.join cmdNames ", "
    $"{groupName}: {namesList}"

  // Compact command list for welcome screen (with logo, minimal text)
  let getCompactCommandList () : String =
    let commands = allCommands ()

    // Group commands by category - just names, no descriptions
    let packageCommands = ["nav"; "ls"; "view"; "tree"; "back"; "search"; "val"; "let"; "fn"; "type"; "rm"]
    let sourceControlCommands = ["branch"; "instance"; "sync"]
    let executionCommands = ["run"; "eval"; "scripts"]
    let installCommands = ["install"; "update"; "uninstall"; "status"; "version"]
    let utilityCommands = ["clear"; "help"; "quit"; "experiments"]

    let packageGroup = formatCompactGroup "Packages" packageCommands
    let sourceControlGroup = formatCompactGroup "Source Control" sourceControlCommands
    let executionGroup = formatCompactGroup "Execution" executionCommands
    let installGroup = formatCompactGroup "Installation" installCommands
    let utilityGroup = formatCompactGroup "Utilities" utilityCommands

    let welcome = "Hello! Let's write some software with Darklang."
    let availableCommands = "Here are the commands available:"
    let compactText = Stdlib.String.join [welcome; ""; availableCommands; ""; packageGroup; sourceControlGroup; executionGroup; installGroup; utilityGroup; ""; "Type 'help' for detailed command info"] "\n"
    let textLines = Stdlib.String.split compactText "\n"
    Logo.combineLogoAndText Logo.logo textLines Colors.logoColor

  // Detailed command list for help command (no logo, full details)
  let getDetailedCommandList () : String =
    let commands = allCommands ()

    // Group commands by category
    let packageCommands = ["nav"; "ls"; "view"; "tree"; "back"; "search"; "val"; "let"; "fn"; "type"; "rm"]
    let sourceControlCommands = ["branch"; "instance"; "sync"; ]
    let executionCommands = ["run"; "eval"; "scripts"]
    let installCommands = ["install"; "update"; "uninstall"; "status"; "version"]
    let utilityCommands = ["clear"; "help"; "quit"; "experiments"]

    // Format command groups
    let packageGroup = formatCommandGroup "Packages" packageCommands commands
    let sourceControlGroup = formatCommandGroup "Source Control" sourceControlCommands commands
    let executionGroup = formatCommandGroup "Execution" executionCommands commands
    let installGroup = formatCommandGroup "Installation" installCommands commands
    let utilityGroup = formatCommandGroup "Utilities" utilityCommands commands

    // Find any ungrouped commands
    let allGroupedCommands =
      Stdlib.List.flatten [ packageCommands; sourceControlCommands; executionCommands; installCommands; utilityCommands ]
    let ungroupedCommands =
      commands
      |> Stdlib.List.filter (fun cmd ->
        match Stdlib.List.findFirst allGroupedCommands (fun name -> name == cmd.name) with
        | Some _ -> false
        | None -> true)
      |> Stdlib.List.map (fun cmd ->
        match cmd.aliases with
        | [] -> $"  {cmd.name} - {cmd.description}"
        | aliases ->
          let aliasText = Stdlib.String.join aliases ", "
          $"  {cmd.name} ({aliasText}) - {cmd.description}")

    let allGroups = [packageGroup; sourceControlGroup; executionGroup; installGroup; utilityGroup]
    let finalGroups =
      if Stdlib.List.isEmpty ungroupedCommands then
        allGroups
      else
        let ungroupedGroup =
          let ungroupedLines = Stdlib.String.join ungroupedCommands "\n"
          $"Other:\n{ungroupedLines}"
        Stdlib.List.append allGroups [ungroupedGroup]

    Stdlib.String.join finalGroups "\n\n"


  let getCompletions (state: AppState) (input: String) : List<String> =
    let parsed = Completion.parseInput input

    if parsed.isCompletingCommand then
      // Completing command name
      let partial = Completion.getPartialCompletion parsed
      let commands = allCommands ()
      let allNames = Stdlib.List.map commands (fun cmd -> cmd.name)
      let allAliases = Stdlib.List.fold commands [] (fun acc cmd -> Stdlib.List.append acc cmd.aliases)
      let allOptions = Stdlib.List.append allNames allAliases
      if Stdlib.String.isEmpty partial then
        allOptions
      else
        Stdlib.List.filter allOptions (fun name -> Stdlib.String.startsWith name partial)
    else
      // Completing command arguments
      let handler = findCommand parsed.commandName
      let completeFunc = handler.complete
      completeFunc state parsed.args

  let getCompletionHint (state: AppState) (input: String) : String =
    let parsed = Completion.parseInput input
    let completions = getCompletions state input
    let partial = Completion.getPartialCompletion parsed
    Completion.getHintFromCompletions partial completions


module Commands =
  let parseAndExecute (state: AppState) (input: String) : AppState =
    let trimmed = Stdlib.String.trim input
    let parts = Stdlib.String.split trimmed " "
    match parts with
    | [] -> state
    | commandName :: args ->
      Registry.executeCommand commandName state args


// TODO: key handling is very long (~170 lines) - consider:
//   - extracting to separate module (input/keyHandler.dark)
//   - using a key binding map/table instead of huge match
//   - separate handlers for different page types
let handleKeyInput (state: AppState) (key: Stdlib.Cli.Stdin.Key.Key) (modifiers: Stdlib.Cli.Stdin.Modifiers.Modifiers) (keyChar: Stdlib.Option.Option<String>) : AppState =
  match state.currentPage with
  | MainPrompt ->
    // Normal command mode key handling
    match key with
    | Enter ->
      // Execute the current command
      if Stdlib.String.isEmpty (Stdlib.String.trim state.mainPrompt) then
        Stdlib.printLine ""
        { state with
            mainPrompt = ""
            cursorPosition = 0L
            needsFullRedraw = false
            historyIndex = -1L }
      else
        Stdlib.printLine ""
        let commandToExecute = Stdlib.String.trim state.mainPrompt
        let newState = Commands.parseAndExecute state commandToExecute
        // Add command to history (avoiding duplicates)
        let updatedHistory =
          if Stdlib.List.isEmpty state.commandHistory || Stdlib.List.head state.commandHistory != Stdlib.Option.Option.Some commandToExecute then
            Stdlib.List.append [commandToExecute] state.commandHistory
          else
            state.commandHistory
        { newState with
            mainPrompt = ""
            cursorPosition = 0L
            needsFullRedraw = true
            commandHistory = updatedHistory
            historyIndex = -1L }
    | Backspace ->
      // Remove character before cursor
      if state.cursorPosition > 0L then
        let beforeCursor = Stdlib.String.slice state.mainPrompt 0L (state.cursorPosition - 1L)
        let afterCursor = Stdlib.String.dropFirst state.mainPrompt state.cursorPosition
        let newPrompt = beforeCursor ++ afterCursor
        { state with
            mainPrompt = newPrompt
            cursorPosition = state.cursorPosition - 1L
            historyIndex = -1L
            needsFullRedraw = false }
      else
        state
    | Delete ->
      // Remove character at cursor position
      let promptLength = Stdlib.String.length state.mainPrompt

      if state.cursorPosition < promptLength then
        let beforeCursor = Stdlib.String.slice state.mainPrompt 0L state.cursorPosition
        let afterCursor = Stdlib.String.dropFirst state.mainPrompt (state.cursorPosition + 1L)
        let newPrompt = beforeCursor ++ afterCursor
        { state with
            mainPrompt = newPrompt
            historyIndex = -1L
            needsFullRedraw = false }
      else
        state
    | Tab ->
      // Handle tab completion
      let completions = Registry.getCompletions state state.mainPrompt

      // CLEANUP: When there are multiple completions with a common prefix (e.g. "filter" and "filterMap"),
      // Tab should complete up to the common part ("filter") rather than just showing options
      match completions with
      | [] ->
        // No completions available
        state
      | [singleCompletion] ->
        // Single completion - auto-complete it
        let parsed = Completion.parseInput state.mainPrompt
        let newPrompt = Completion.buildCompletedCommand parsed singleCompletion
        let newPromptLength = Stdlib.String.length newPrompt
        { state with
            mainPrompt = newPrompt
            cursorPosition = newPromptLength }
      | multiple ->
        // Multiple completions - show them
        Stdlib.printLine ""
        Stdlib.printLine (Stdlib.String.join multiple " ")
        { state with needsFullRedraw = true }
    | UpArrow ->
      // Navigate to previous command in history
      if Stdlib.Bool.not (Stdlib.List.isEmpty state.commandHistory) then
        let historyLength = Stdlib.List.length state.commandHistory
        let newIndex =
          if state.historyIndex == -1L then
            0L  // Start from the most recent command
          else
            if state.historyIndex < (historyLength - 1L) then
              state.historyIndex + 1L
            else
              state.historyIndex  // Stay at oldest command
        match Stdlib.List.getAt state.commandHistory newIndex with
        | Some command ->
          let commandLength = Stdlib.String.length command
          let newSavedPrompt =
            if state.historyIndex == -1L then
              state.mainPrompt
            else
              state.savedPrompt
          { state with
              mainPrompt = command
              cursorPosition = commandLength
              historyIndex = newIndex
              needsFullRedraw = false
              savedPrompt = newSavedPrompt }
        | None ->
          state
      else
        state
    | DownArrow ->
      // Navigate to next command in history
      if Stdlib.Bool.not (Stdlib.List.isEmpty state.commandHistory) && state.historyIndex >= 0L then
        let newIndex =
          if state.historyIndex > 0L then
            state.historyIndex - 1L
          else
            -1L  // Go back to empty prompt
        if newIndex == -1L then
          let restoredPromptLength = Stdlib.String.length state.savedPrompt
          { state with
              mainPrompt = state.savedPrompt
              cursorPosition = restoredPromptLength
              historyIndex = newIndex
              needsFullRedraw = false }
        else
          match Stdlib.List.getAt state.commandHistory newIndex with
          | Some command ->
            let commandLength = Stdlib.String.length command
            { state with
                mainPrompt = command
                cursorPosition = commandLength
                historyIndex = newIndex
                needsFullRedraw = false }
          | None ->
            state
      else
        state
    | LeftArrow ->
      // Move cursor left (cursor-only movement, no content change)
      let newCursorPos = Stdlib.Int64.max 0L (state.cursorPosition - 1L)
      { state with cursorPosition = newCursorPos; needsFullRedraw = false }
    | RightArrow ->
      // Move cursor right
      let promptLength = Stdlib.String.length state.mainPrompt
      let newCursorPos = Stdlib.Int64.min promptLength (state.cursorPosition + 1L)
      { state with cursorPosition = newCursorPos; needsFullRedraw = false }
    | Home ->
      // Move cursor to start of line
      { state with cursorPosition = 0L; needsFullRedraw = false }
    | End ->
      // Move cursor to end of line
      let promptLength = Stdlib.String.length state.mainPrompt
      { state with cursorPosition = promptLength; needsFullRedraw = false }
    | _ ->
      // Add character to prompt at cursor position
      match keyChar with
      | Some char ->
        let beforeCursor = Stdlib.String.slice state.mainPrompt 0L state.cursorPosition
        let afterCursor = Stdlib.String.dropFirst state.mainPrompt state.cursorPosition
        let newPrompt = beforeCursor ++ char ++ afterCursor
        { state with
            mainPrompt = newPrompt
            cursorPosition = state.cursorPosition + (Stdlib.String.length char)
            historyIndex = -1L
            needsFullRedraw = false
            savedPrompt = "" }
      | None ->
        state
  | Experiments ->
    // Experiments page key handling
    match key with
    | Escape ->
      // Exit experiments page back to main prompt
      { state with currentPage = Page.MainPrompt; needsFullRedraw = true }
    | _ ->
      // Ignore other keys in experiments mode
      state
  | InteractiveNav navState ->
    // Interactive navigation key handling
    Packages.Nav.Interactive.handleKey state key keyChar navState

module Update =
  let updateAppState (state: AppState) (msg: Msg) : AppState =
    match msg with
    | ProcessInput input ->
      Commands.parseAndExecute state input
    | KeyPressed (key, modifiers, keyChar) ->
      handleKeyInput state key modifiers keyChar
    | Exit ->
      { state with isExiting = true }

  let processInput (state: AppState) (input: String) : AppState =
    let trimmedInput = Stdlib.String.trim input
    if Stdlib.String.isEmpty trimmedInput then
      state
    else
      let msg = Msg.ProcessInput trimmedInput
      updateAppState state msg


let runInteractiveLoop (state: AppState) : Int64 =
  if state.isExiting then
    0L
  else
    // Display current page
    match state.currentPage with
    | MainPrompt ->
      // Determine where user input starts (branch + location + " > ")
      let locationLength =
        let pathStr = Packages.formatLocation state.packageData.currentLocation
        if Stdlib.String.isEmpty pathStr then
          0L
        else
          (Stdlib.String.length pathStr) + 1L  // +1 for the space before path

      let inputStartColumn =
        (Stdlib.String.length (View.promptBranchPreamble state))
        + locationLength
        + (Stdlib.String.length "> ")
        + 1L

      if state.needsFullRedraw then
        // Redraw entire prompt line (used after commands that change state)
        let promptText = View.formatPromptWithInput state
        Stdlib.print promptText
      else
        // Update only the input portion (used during typing)
        Stdlib.print "\u001b[?25l" // Hide cursor during redraw
        Stdlib.print (Colors.moveCursorToColumn inputStartColumn)
        Stdlib.print Colors.clearLine
        Stdlib.print state.mainPrompt
        let hint = Registry.getCompletionHint state state.mainPrompt
        if Stdlib.Bool.not (Stdlib.String.isEmpty hint) then
          Stdlib.print (Colors.hint hint)
        Stdlib.print "\u001b[?25h" // Show cursor again

      // Position cursor at correct location within user input
      let cursorColumn = inputStartColumn + state.cursorPosition
      Stdlib.print (Colors.moveCursorToColumn cursorColumn)
    | Experiments ->
      if state.needsFullRedraw then
        Stdlib.printLine "ðŸ§ª Experiments Mode"
        Stdlib.printLine ""
        Stdlib.printLine "Try out various WIP CLI experiments here"
        Stdlib.printLine ""
        Stdlib.printLine "Press ESC to return to main prompt"
    | InteractiveNav navState ->
      // Always display in interactive nav mode - it manages its own screen clearing
      Packages.Nav.Interactive.display navState

    // Read keystroke input
    let keyInput = Stdlib.Cli.Stdin.readKey ()
    let keyPressedMsg = Msg.KeyPressed (keyInput.key, keyInput.modifiers, Stdlib.Option.Option.Some keyInput.keyChar)
    let newState = Update.updateAppState state keyPressedMsg
    runInteractiveLoop newState


let executeCliCommand (args: List<String>) : Int64 =
  let initialState = initState ()

  match args with
  // If someone runs `dark` without args, start the interactive loop
  | [] ->
    Stdlib.printLine (View.formatWelcome ())
    runInteractiveLoop initialState
  // Otherwise, just execute command, print result, and exit
  | _ ->
    let command = args |> Stdlib.String.join " "
    let finalState = Update.processInput initialState command
    0L

