module Darklang.Cli.Packages.Branch

let displayBranchInfo (branch: SCM.Branch.Branch) : Unit =
  let mergedAtText =
    match branch.mergedAt with
    | Some dt -> Stdlib.DateTime.toString dt
    | None -> "(not merged)"

  let ownerName =
    (Builtin.pmGetAccountNameById branch.owner)
    |> Stdlib.Option.withDefault (Stdlib.Uuid.toString branch.owner)

  [ ""
    (Colors.boldText "Branch Information:")
    ""
    $"  ID:             {Stdlib.Uuid.toString branch.id}"
    $"  Name:           {branch.name}"
    $"  Owner:          {ownerName}"
    $"  Created at:     {Stdlib.DateTime.toString branch.createdAt}"
    $"  Merged at:      {mergedAtText}"
    "" ]
  |> Stdlib.printLines


let execute (state: Cli.AppState) (args: List<String>) : Cli.AppState =
  match args with
  | [] -> help state

  | ["list"] ->
    match state.accountID with
    | None ->
      Stdlib.printLine (Colors.error "No account logged in")
      state
    | Some accountID ->
      let branches = Darklang.SCM.Branch.list accountID
      Stdlib.printLine ""
      Stdlib.printLine (Colors.boldText "Branches:")

      branches
      |> Stdlib.List.iter (fun b ->
        let marker =
          match state.currentBranchId with
          | Some currentId -> if b.id == currentId then "* " else "  "
          | None -> "  "

        // Show creation date for disambiguation when there are name collisions
        let dateStr = Stdlib.DateTime.toString b.createdAt
        Stdlib.printLine $"{marker}{b.name} (created {dateStr})")

      Stdlib.printLine ""
      state

  | ["create"; name] ->
    match state.accountID with
    | None ->
      Stdlib.printLine (Colors.error "No account logged in")
      state
    | Some accountID ->
      let branch = Darklang.SCM.Branch.create accountID name
      Stdlib.printLine $"Created and switched to branch: {name}"

      { state with
          currentBranchId = Stdlib.Option.Option.Some branch.id
          needsFullRedraw = true }

  | ["switch"; name] ->
    match state.accountID with
    | None ->
      Stdlib.printLine (Colors.error "No account logged in")
      state
    | Some accountID ->
      let branch = Darklang.SCM.Branch.findByName accountID name

      match branch with
      | None ->
        Stdlib.printLine (Colors.error $"Branch not found: {name}")
        state

      | Some branch ->
        Stdlib.printLine $"Switched to branch: {name}"

        { state with
            currentBranchId = Stdlib.Option.Option.Some branch.id
            needsFullRedraw = true }

  | ["switch-id"; branchIdStr] ->
    match Stdlib.Uuid.parse branchIdStr with
    | Ok branchID ->
      let branch = Darklang.SCM.Branch.get branchID

      match branch with
      | Some b ->
        Stdlib.printLine $"Switched to branch: {b.name}"

        { state with
            currentBranchId = Stdlib.Option.Option.Some b.id
            needsFullRedraw = true }

      | None ->
        Stdlib.printLine (Colors.error $"Branch not found: {branchIdStr}")
        state

    | Error _ ->
      Stdlib.printLine (Colors.error $"Invalid branch ID: {branchIdStr}")
      state

  | ["status"] ->
    let currentBranch =
      match state.currentBranchId with
      | Some branchID -> Darklang.SCM.Branch.get branchID
      | None -> Stdlib.Option.Option.None

    match currentBranch with
    | Some branch ->
      [ ""
        $"Branch: {branch.name}"
        $"ID: {Stdlib.Uuid.toString branch.id}"
        "" ]
      |> Stdlib.printLines

    | None -> Stdlib.printLine (Colors.error "Not currently on a branch. Use 'branch switch <name>' to switch to one, or 'branch list' to see available branches.")

    state

  | ["info"] ->
    let currentBranch =
      match state.currentBranchId with
      | Some branchID -> Darklang.SCM.Branch.get branchID
      | None -> Stdlib.Option.Option.None

    match currentBranch with
    | Some branch ->
      displayBranchInfo branch
      state

    | None ->
      Stdlib.printLine (Colors.error "Not currently on a branch. Use 'branch switch <name>' to switch to one, or 'branch list' to see available branches.")
      state

  | ["info"; name] ->
    match state.accountID with
    | None ->
      Stdlib.printLine (Colors.error "No account logged in")
      state
    | Some accountID ->
      let branch = Darklang.SCM.Branch.findByName accountID name

      match branch with
      | None ->
        Stdlib.printLine (Colors.error $"Branch not found: {name}")
        state

      | Some branch ->
        displayBranchInfo branch
        state

  | ["clear"]
  | ["unset"]
  | ["none"] ->
    Stdlib.printLine "Cleared current branch (now searching all packages)"

    { state with
        currentBranchId = Stdlib.Option.Option.None
        needsFullRedraw = true }

  | _ ->
    Stdlib.printLine (Colors.error "Unknown branch command")
    help state


let help (state: Cli.AppState) : Cli.AppState =
  let lines =
    [ "Branch Management Commands"
      ""
      "  branch list                   List all branches"
      "  branch create NAME            Create and switch to new branch"
      "  branch switch NAME            Switch to existing branch"
      "  branch switch-id ID           Switch to branch by ID"
      "  branch status                 Show current branch status"
      "  branch info                   Show detailed info (current branch)"
      "  branch info NAME              Show detailed info (specific branch)"
      "  branch [clear|unset|none]     Clear current branch (search all packages)"
      "" ]

  Stdlib.List.iter lines Stdlib.printLine
  state


let complete (state: Cli.AppState) (args: List<String>) : List<Cli.Completion.CompletionItem> =
  match args with
  | [] ->
    [ "list"; "create"; "switch"; "switch-id"; "status"; "unset"; "none"; "clear" ]
    |> Stdlib.List.map Cli.Completion.simple

  | [partial] ->
    [ "list"; "create"; "switch"; "switch-id"; "status"; "unset"; "none"; "clear" ]
    |> Stdlib.List.filter (fun cmd -> Stdlib.String.startsWith cmd partial)
    |> Stdlib.List.map Cli.Completion.simple

  | ["switch"] ->
    match state.accountID with
    | None -> []
    | Some accountID ->
      let branches = Darklang.SCM.Branch.list accountID
      branches
      |> Stdlib.List.map (fun b -> b.name)
      |> Stdlib.List.map Cli.Completion.simple

  | ["switch"; partial] ->
    match state.accountID with
    | None -> []
    | Some accountID ->
      let branches = Darklang.SCM.Branch.list accountID

      branches
      |> Stdlib.List.map (fun b -> b.name)
      |> Stdlib.List.filter (fun name -> Stdlib.String.startsWith name partial)
      |> Stdlib.List.map Cli.Completion.simple

  | _ -> []
