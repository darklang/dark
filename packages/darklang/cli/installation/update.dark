module Darklang.Cli.Installation.Update


let execute (state: AppState) (args: List<String>) : AppState =
  let currentVersion = Helpers.cliVersion ()
  let currentMode = Installation.System.getInstallationMode ()
  let host = (Stdlib.Cli.Host.getRuntimeHost ()) |> Builtin.unwrap

  // Parse flags
  let autoConfirm =
    (Stdlib.List.member_v0 args "--yes") || (Stdlib.List.member_v0 args "-y")

  match currentMode with
  | Installed ->
    Stdlib.printLine $"Checking for updates from Darklang CLI {currentVersion}..."
    match Installation.System.updateIfAvailable currentVersion host autoConfirm with
    | Ok message ->
      Stdlib.printLine (View.formatSuccess message)
      state
    | Error e ->
      Stdlib.printLine (View.formatError e)
      state
  | Portable ->
    let message = $"Running in portable mode from {Builtin.directoryCurrent ()}/.darklang\n\nTo update this portable executable, please download the latest release manually from:\nhttps://github.com/darklang/dark/releases\n\nFor automatic updates, consider running 'install' to set up global installation."
    Stdlib.printLine (View.formatSuccess message)
    state


let complete (_state: AppState) (_args: List<String>) : List<Completion.CompletionItem> =
  []


let help (state: AppState) : Unit =
  [ "Usage: update [--yes|-y]"
    "Update Darklang CLI to the latest version."
    ""
    "Checks GitHub for the latest release and updates the globally"
    "installed CLI. Only works for global installations."
    ""
    "Flags:"
    "  --yes, -y  Skip confirmation prompt" ]
  |> Stdlib.printLines