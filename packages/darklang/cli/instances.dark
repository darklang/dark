module Darklang.Cli.Instances

let execute (state: Cli.AppState) (args: List<String>) : Cli.AppState =
  match args with
  | [] -> help state

  | ["list"] ->
    let instances = SCM.Instances.list ()

    if Stdlib.List.isEmpty instances then
      [ ""
        "No instances configured."
        ""
        "Use 'instance add <name> <url>' to add an instance." ]
      |> Stdlib.printLines
    else
      [ ""
        (Colors.boldText "Configured Instances:")
        "" ]
      |> Stdlib.printLines

      instances
      |> Stdlib.List.iter (fun instance ->
        let idShort = Stdlib.String.slice (Stdlib.Uuid.toString instance.id) 0L 8L

        [ $"  {instance.name}"
          $"    ID:  {idShort}..."
          $"    URL: {instance.url}"
          "" ]
        |> Stdlib.printLines)

    state

  | ["add"; name; url] ->
    // Check if instance with this name already exists
    match Darklang.SCM.Instances.getByName name with
    | Some existing ->
      [ (Colors.error $"Instance with name '{name}' already exists")
        $"  ID:  {Stdlib.Uuid.toString existing.id}"
        $"  URL: {existing.url}" ]
      |> Stdlib.printLines

      state

    | None ->
      let instance = Darklang.SCM.Instances.add name url

      [ (Colors.success $"Added instance: {name}")
        $"  ID:  {Stdlib.Uuid.toString instance.id}"
        $"  URL: {url}" ]
      |> Stdlib.printLines

      state

  | ["remove"; name] ->
    match Darklang.SCM.Instances.removeByName name with
    | Ok () ->
      Stdlib.printLine (Colors.success $"Removed instance: {name}")
      state

    | Error msg ->
      Stdlib.printLine (Colors.error msg)
      state

  | ["info"; name] ->
    match Darklang.SCM.Instances.getByName name with
    | Some instance ->
      [ ""
        (Colors.boldText "Instance Information:")
        ""
        $"  Name: {instance.name}"
        $"  ID:   {Stdlib.Uuid.toString instance.id}"
        $"  URL:  {instance.url}"
        "" ]
      |> Stdlib.printLines

      state

    | None ->
      Stdlib.printLine (Colors.error $"Instance not found: {name}")
      state

  | _ ->
    Stdlib.printLine (Colors.error "Invalid arguments")
    Stdlib.printLine ""
    help state


let help (state: Cli.AppState) : Cli.AppState =
  [ "Instance Management Commands"
    ""
    "  instance list              List all configured instances"
    "  instance add NAME URL      Add a new instance"
    "  instance remove NAME       Remove an instance"
    "  instance info NAME         Show detailed instance info"
    ""
    "Examples:"
    "  instance add local http://dark-packages.dlio.localhost:11001"
    "  instance add production https://packages.darklang.com"
    "  instance list"
    "  instance remove local"
    "" ]
  |> Stdlib.printLines

  state


let complete (state: Cli.AppState) (args: List<String>) : List<String> =
  match args with
  | [] -> ["list"; "add"; "remove"; "info"]

  | [partial] ->
    ["list"; "add"; "remove"; "info"]
    |> Stdlib.List.filter (fun cmd -> Stdlib.String.startsWith cmd partial)

  | ["remove"] ->
    let instances = Darklang.SCM.Instances.list ()
    Stdlib.List.map instances (fun i -> i.name)

  | ["remove"; partial] ->
    let instances = Darklang.SCM.Instances.list ()

    instances
    |> Stdlib.List.map (fun i -> i.name)
    |> Stdlib.List.filter (fun name -> Stdlib.String.startsWith name partial)

  | ["info"] ->
    let instances = Darklang.SCM.Instances.list ()
    Stdlib.List.map instances (fun i -> i.name)

  | ["info"; partial] ->
    let instances = Darklang.SCM.Instances.list ()

    instances
    |> Stdlib.List.map (fun i -> i.name)
    |> Stdlib.List.filter (fun name -> Stdlib.String.startsWith name partial)

  | _ -> []
