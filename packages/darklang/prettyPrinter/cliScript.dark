module Darklang.PrettyPrinter.ProgramTypes

let sourceFile
  (source: LanguageTools.ProgramTypes.SourceFile.SourceFile)
  : String =
  let ctx =
    PrettyPrinter.ProgramTypes.Context
      { currentModule = Stdlib.Option.Option.None
        currentFunction = Stdlib.Option.Option.None }

  let declsPart =
    (Stdlib.List.fold source.declarations [] (fun acc decl ->
      match decl with
      | Module moduleDecl ->
        let prettyPrinted = PrettyPrinter.definitions ctx moduleDecl
        acc |> Stdlib.List.push prettyPrinted

      | Type typeDecl ->
        let prettyPrinted = PrettyPrinter.ProgramTypes.packageType typeDecl
        acc |> Stdlib.List.push prettyPrinted

      | Value valDecl ->
        let prettyPrinted = PrettyPrinter.ProgramTypes.packageValue ctx valDecl
        acc |> Stdlib.List.push prettyPrinted

      | Function fnDecl ->
        let prettyPrinted = PrettyPrinter.ProgramTypes.packageFn ctx fnDecl
        acc |> Stdlib.List.push prettyPrinted))
    |> Stdlib.List.reverse

  let exprsPart =
    (Stdlib.List.fold source.exprsToEval [] (fun acc expr ->
      let prettyPrinted = PrettyPrinter.ProgramTypes.expr ctx expr
      acc |> Stdlib.List.push prettyPrinted))
    |> Stdlib.List.reverse

  [ declsPart; exprsPart ] |> Stdlib.List.flatten |> Stdlib.String.join "\n\n"