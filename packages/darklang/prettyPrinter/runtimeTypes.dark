module Darklang =
  module PrettyPrinter =
    module RuntimeTypes =
      let nameResolutionError
        (nr: PACKAGE.Darklang.LanguageTools.RuntimeErrors.NameResolution.Error)
        : String =
        PACKAGE.Darklang.Stdlib.String.join nr.names "."


      module FQName =
        let builtIn
          (namePart: String)
          (t: PACKAGE.Darklang.LanguageTools.RuntimeTypes.FQName.BuiltIn)
          : String =
          let modulesPart =
            match t.modules with
            | [] -> ""
            | modules ->
              modules
              |> PACKAGE.Darklang.Stdlib.String.join "."
              |> (fun parts -> $"{parts}.")

          let versionPart =
            if t.version == 0L then
              ""
            else
              $"_v{PACKAGE.Darklang.Stdlib.Int.toString t.version}"

          $"{modulesPart}{namePart}{versionPart}"


        let userProgram
          (namePart: String)
          (u: PACKAGE.Darklang.LanguageTools.RuntimeTypes.FQName.UserProgram)
          : String =
          let modulesPart =
            match u.modules with
            | [] -> ""
            | modules ->
              modules
              |> PACKAGE.Darklang.Stdlib.String.join "."
              |> (fun parts -> $"{parts}.")

          let versionPart =
            if u.version == 0L then
              ""
            else
              $"_v{PACKAGE.Darklang.Stdlib.Int.toString u.version}"

          $"{modulesPart}{namePart}{versionPart}"


        let package
          (namePart: String)
          (p: PACKAGE.Darklang.LanguageTools.RuntimeTypes.FQName.Package)
          : String =
          let modulesPart = PACKAGE.Darklang.Stdlib.String.join p.modules "."

          let versionPart =
            if p.version == 0L then
              ""
            else
              $"_v{PACKAGE.Darklang.Stdlib.Int.toString p.version}"

          $"PACKAGE.{p.owner}.{modulesPart}.{namePart}{versionPart}"


      module TypeName =
        let name
          (name: PACKAGE.Darklang.LanguageTools.RuntimeTypes.TypeName.Name)
          : String =
          match name with
          | TypeName name -> name

        let builtIn
          (t: PACKAGE.Darklang.LanguageTools.RuntimeTypes.TypeName.BuiltIn)
          : String =
          FQName.builtIn (name t.name) t

        let userProgram
          (u: PACKAGE.Darklang.LanguageTools.RuntimeTypes.TypeName.UserProgram)
          : String =
          FQName.userProgram (name u.name) u

        let package
          (p: PACKAGE.Darklang.LanguageTools.RuntimeTypes.TypeName.Package)
          : String =
          FQName.package (name p.name) p


      let typeName
        (t: PACKAGE.Darklang.LanguageTools.RuntimeTypes.TypeName.TypeName)
        : String =
        match t with
        | BuiltIn b -> TypeName.builtIn b
        | UserProgram u -> TypeName.userProgram u
        | Package p -> TypeName.package p

      // module ConstantName =
      //   let name
      //     (name: PACKAGE.Darklang.LanguageTools.ProgramTypes.ConstantName.Name)
      //     : String =
      //     match name with
      //     | ConstantName name -> name

      //   module BuiltIn =
      //     let fullForReference
      //       (c: PACKAGE.Darklang.LanguageTools.ProgramTypes.ConstantName.BuiltIn)
      //       : String =
      //       PACKAGE.Darklang.PrettyPrinter.ProgramTypes.FQName.BuiltIn.fullForReference
      //         (PACKAGE.Darklang.PrettyPrinter.ProgramTypes.ConstantName.name c.name)
      //         c

      //   module UserProgram =

      //     let fullForReference
      //       (u: PACKAGE.Darklang.LanguageTools.ProgramTypes.ConstantName.UserProgram)
      //       : String =
      //       PACKAGE.Darklang.PrettyPrinter.ProgramTypes.FQName.UserProgram.fullForReference
      //         (PACKAGE.Darklang.PrettyPrinter.ProgramTypes.ConstantName.name u.name)
      //         u

      //   module Package =
      //     let fullForReference
      //       (p: PACKAGE.Darklang.LanguageTools.ProgramTypes.ConstantName.Package)
      //       : String =
      //       PACKAGE.Darklang.PrettyPrinter.ProgramTypes.FQName.Package.fullForReference
      //         (PACKAGE.Darklang.PrettyPrinter.ProgramTypes.ConstantName.name p.name)
      //         p

      // let constantName
      //   (t: PACKAGE.Darklang.LanguageTools.ProgramTypes.ConstantName.ConstantName)
      //   : String =
      //   match t with
      //   | BuiltIn b ->
      //     PACKAGE.Darklang.PrettyPrinter.ProgramTypes.ConstantName.BuiltIn.fullForReference
      //       b
      //   | UserProgram u ->
      //     PACKAGE.Darklang.PrettyPrinter.ProgramTypes.ConstantName.UserProgram.fullForReference
      //       u
      //   | Package p ->
      //     PACKAGE.Darklang.PrettyPrinter.ProgramTypes.ConstantName.Package.fullForReference
      //       p


      module FnName =
        let name
          (name: PACKAGE.Darklang.LanguageTools.RuntimeTypes.FnName.Name)
          : String =
          match name with
          | FnName name -> name

        let builtIn
          (f: PACKAGE.Darklang.LanguageTools.RuntimeTypes.FnName.BuiltIn)
          : String =
          FQName.builtIn (name f.name) f

        let userProgram
          (u: PACKAGE.Darklang.LanguageTools.RuntimeTypes.FnName.UserProgram)
          : String =
          FQName.userProgram (name u.name) u

        let package
          (p: PACKAGE.Darklang.LanguageTools.RuntimeTypes.FnName.Package)
          : String =
          FQName.package (name p.name) p

      let fnName
        (t: PACKAGE.Darklang.LanguageTools.RuntimeTypes.FnName.FnName)
        : String =
        match t with
        | BuiltIn b -> FnName.builtIn b
        | UserProgram u -> FnName.userProgram u
        | Package p -> FnName.package p

      let typeReference
        (t: PACKAGE.Darklang.LanguageTools.RuntimeTypes.TypeReference)
        : String =
        match t with
        | TUnit -> "Unit"
        | TBool -> "Bool"
        | TInt -> "Int"
        | TInt8 -> "Int8"
        | TUInt8 -> "UInt8"
        | TInt16 -> "Int16"
        | TUInt16 -> "UInt16"
        | TInt128 -> "Int128"
        | TUInt128 -> "UInt128"
        | TFloat -> "Float"
        | TChar -> "Char"
        | TString -> "String"
        | TUuid -> "Uuid"
        | TBytes -> "Bytes"
        | TDateTime -> "DateTime"

        | TList inner -> $"List<{typeReference inner}>"
        | TDict inner -> $"Dict<{typeReference inner}>"
        | TTuple(first, second, theRest) ->
          (PACKAGE.Darklang.Stdlib.List.append [ first; second ] theRest)
          |> PACKAGE.Darklang.Stdlib.List.map (fun item -> typeReference item)
          |> PACKAGE.Darklang.Stdlib.String.join " * "
          |> fun parts -> "(" ++ parts ++ ")"

        | TCustomType(typ, args) ->
          let argsPart =
            match args with
            | [] -> ""
            | args ->
              args
              |> PACKAGE.Darklang.Stdlib.List.map (fun arg -> typeReference arg)
              |> PACKAGE.Darklang.Stdlib.String.join ", "
              |> fun parts -> $"<{parts}>"

          let typeNamePart =
            match typ with
            | Ok name -> typeName name
            | Error e -> nameResolutionError e

          $"{typeNamePart}{argsPart}"

        | TFn(args, ret) ->
          let argPart =
            args
            |> PACKAGE.Darklang.Stdlib.List.map (fun arg -> typeReference arg)
            |> PACKAGE.Darklang.Stdlib.String.join ", "
            |> fun parts -> "(" ++ parts ++ ")"

          $"{argPart} -> {typeReference ret}"

        | TDB inner -> $"DB<{typeReference inner}>"

        | TVariable varName -> "'" ++ varName


      let knownType
        (t: PACKAGE.Darklang.LanguageTools.RuntimeTypes.KnownType)
        : String =
        match t with
        | KTUnit -> "Unit"
        | KTBool -> "Bool"
        | KTInt -> "Int"
        | KTInt8 -> "Int8"
        | KTUInt8 -> "UInt8"
        | KTInt16 -> "Int16"
        | KTUInt16 -> "UInt16"
        | KTInt128 -> "Int128"
        | KTUInt128 -> "UInt128"
        | KTFloat -> "Float"
        | KTChar -> "Char"
        | KTString -> "String"
        | KTDateTime -> "DateTime"
        | KTUuid -> "Uuid"
        | KTBytes -> "Bytes"

        | KTList typ -> $"List<{valueType typ}>"
        | KTDict typ -> $"Dict<{valueType typ}>"
        | KTTuple(t1, t2, trest) ->
          (PACKAGE.Darklang.Stdlib.List.append [ t1; t2 ] trest)
          |> PACKAGE.Darklang.Stdlib.List.map (fun item -> valueType item)
          |> PACKAGE.Darklang.Stdlib.String.join ", "
          |> fun s -> $"({s})"

        | KTFn(argTypes, retType) ->
          (Stdlib.List.push argTypes retType)
          |> PACKAGE.Darklang.Stdlib.List.map (fun vt -> valueType vt)
          |> PACKAGE.Darklang.Stdlib.String.join " -> "

        | KTCustomType(name, typeArgs) ->
          let typeArgsPortion =
            match typeArgs with
            | [] -> ""
            | args ->
              args
              |> PACKAGE.Darklang.Stdlib.List.map (fun t -> valueType t)
              |> PACKAGE.Darklang.Stdlib.String.join ", "
              |> fun betweenBrackets -> "<" + betweenBrackets + ">"

          (typeName name) ++ typeArgsPortion

        | KTDB typ -> $"Datastore<{valueType typ}>"


      let valueType
        (vt: PACKAGE.Darklang.LanguageTools.RuntimeTypes.ValueType)
        : String =
        match vt with
        | Known kt -> knownType kt
        | Unknown -> "_"


      module Dval =
        let valueTypeName
          (dv: PACKAGE.Darklang.LanguageTools.RuntimeTypes.Dval.Dval)
          : String =
          dv |> LanguageTools.RuntimeTypes.Dval.toValueType |> valueType

        let makeSpaces (len: Int) : String =
          (PACKAGE.Darklang.Stdlib.List.repeat len " ")
          |> Builtin.unwrap
          |> PACKAGE.Darklang.Stdlib.String.join ""

        let withIndent
          (indent: Int)
          (dv: PACKAGE.Darklang.LanguageTools.RuntimeTypes.Dval.Dval)
          : String =
          let nl = "\n" ++ (makeSpaces indent)
          let inl = "\n" ++ (makeSpaces (indent + 2L))
          let indent = indent + 2L

          let valueTypeName = valueTypeName dv

          match dv with
          | DUnit -> "()"

          | DBool true -> "true"
          | DBool false -> "false"

          | DChar c ->
            let c = Stdlib.Char.toString c
            $"'{c}'"
          | DString s -> $"\"{s}\""

          | DInt i -> Builtin.Int.toString i
          | DInt8 i -> Builtin.Int8.toString i
          | DUInt8 i -> Builtin.UInt8.toString i
          | DInt16 i -> Builtin.Int16.toString i
          | DUInt16 i -> Builtin.UInt16.toString i
          | DInt128 i -> Builtin.Int128.toString i
          | DUInt128 i -> Builtin.UInt128.toString i

          | DFloat f -> PACKAGE.Darklang.Stdlib.Float.toString f // CLEANUP: deal with Infinity, NegativeInfinity, and NaN

          | DDateTime d ->
            $"<{valueTypeName}: {PACKAGE.Darklang.Stdlib.DateTime.toString d}>"

          | DBytes bytes ->
            //Base64.defaultEncodeToString bytes
            "<Bytes>"

          | DUuid uuid ->
            $"<{valueTypeName}: {PACKAGE.Darklang.Stdlib.Uuid.toString uuid}>"



          | DList(vt, l) ->
            if PACKAGE.Darklang.Stdlib.List.isEmpty l then
              $"{valueTypeName} []"
            else
              let elems =
                PACKAGE.Darklang.Stdlib.String.join
                  (PACKAGE.Darklang.Stdlib.List.map l (fun item ->
                    withIndent indent item))
                  ", "

              $"[{inl}{elems}{nl}]"

          | DTuple(first, second, theRest) ->
            let l = PACKAGE.Darklang.Stdlib.List.append [ first; second ] theRest

            let parts =
              PACKAGE.Darklang.Stdlib.List.map l (fun item -> withIndent indent item)

            let short = PACKAGE.Darklang.Stdlib.String.join parts ", "

            if PACKAGE.Darklang.Stdlib.String.length short <= 80L then
              $"({short})"
            else
              let long = PACKAGE.Darklang.Stdlib.String.join parts $"{inl}, "

              $"({inl}{long}{nl})"

          | DRecord(_, typeName, typeArgs, o) ->
            let strs =
              o
              |> PACKAGE.Darklang.Stdlib.Dict.toList
              |> PACKAGE.Darklang.Stdlib.List.map (fun pair ->
                let (key, value) = pair
                $"{key}: {withIndent indent value}")

            let elems = PACKAGE.Darklang.Stdlib.String.join strs $",{inl}"
            let typeStr = typeName typeName

            let typeArgsPart =
              match typeArgs with
              | [] -> ""
              | args ->
                args
                |> PACKAGE.Darklang.Stdlib.List.map (fun t -> valueTypeName t)
                |> PACKAGE.Darklang.Stdlib.String.join ", "
                |> fun betweenBrackets -> "<" + betweenBrackets + ">"

            typeStr ++ typeArgsPart ++ " {" ++ inl ++ elems ++ nl ++ "}"

          | DDict d ->
            if d == PACKAGE.Darklang.Stdlib.Dict.empty then
              "{}"
            else
              let strs =
                d
                |> PACKAGE.Darklang.Stdlib.Dict.toList
                |> PACKAGE.Darklang.Stdlib.List.map (fun pair ->
                  let (key, value) = pair
                  $"{key}: {withIndent indent value}")

              let elems = PACKAGE.Darklang.Stdlib.String.join strs $",{inl}"
              "{" ++ inl ++ elems ++ nl ++ "}"

          | DEnum(_, typeName, typeArgs, caseName, fields) ->
            let typeArgsPart =
              match typeArgs with
              | [] -> ""
              | typeArgs ->
                typeArgs
                |> PACKAGE.Darklang.Stdlib.List.map (fun typeArg ->
                  valueType typeArg)
                |> PACKAGE.Darklang.Stdlib.String.join ", "
                |> fun parts -> $"<{parts}>"

            let short =
              let fieldStr =
                fields
                |> PACKAGE.Darklang.Stdlib.List.map (fun value ->
                  withIndent indent value)
                |> PACKAGE.Darklang.Stdlib.String.join ", "

              let fieldStr = if fieldStr == "" then "" else $"({fieldStr})"

              let typeStr = typeName typeName
              $"{typeStr}{typeArgsPart}.{caseName}{fieldStr}"

            if PACKAGE.Darklang.Stdlib.String.length short <= 80L then
              short
            else
              let fieldStr =
                fields
                |> PACKAGE.Darklang.Stdlib.List.map (fun value ->
                  withIndent indent value)
                |> PACKAGE.Darklang.Stdlib.String.join $",{inl}"

              let fieldStr = if fieldStr == "" then "" else $"({inl}{fieldStr}{nl})"

              let typeStr = typeName typeName
              $"{typeStr}{typeArgsPart}.{caseName}{fieldStr}"


          | DFnVal(NamedFn fnName) ->
            PACKAGE.Darklang.PrettyPrinter.RuntimeTypes.FnName.fnName fnName

          | DFnVal(Lambda impl) ->
            // Note: this use case is safe (RE docs/dblock-serialization.md)
            let ps =
              impl.parameters
              |> Stdlib.List.map Stdlib.Tuple2.second
              |> Stdlib.String.join ", "

            // TODO
            // let body = impl.body |> RuntimeTypes.Expr.toString
            $"\\ {ps} {{ ... }}"


          | DDB name -> $"<{valueTypeName}: {name}>"



      let dval (dv: PACKAGE.Darklang.LanguageTools.RuntimeTypes.Dval.Dval) : String =
        Dval.withIndent 0L dv