// aliases and helper fns
type Point = PACKAGE.Darklang.LanguageTools.Parser.Point
type Range = PACKAGE.Darklang.LanguageTools.Parser.Range
type ParsedNode = PACKAGE.Darklang.LanguageTools.Parser.ParsedNode

let range (s: Int64 * Int64) (e: Int64 * Int64) : Range =
  let (startRow, startColumn) = s
  let (endRow, endColumn) = e

  Range
    { start = Point { row = startRow; column = startColumn }
      end_ = Point { row = endRow; column = endColumn } }



/// These tests are a huge pain to write and maintain
/// Let's focus on roundtripping tests, largely,
/// and just have one of these for some base-line checking.
///
/// TODO: make that one test much more comprehensive
module ParseToSimplifiedTree =
  // super basic test just to make sure we don't throw an exception
  (let parsed =
    Builtin.parserParseToSimplifiedTree
      "let add (a: Int) (b: Int): Int =\n  let sum = a + b\n  sum"

   parsed.typ) = "source_file"


  // simplest type alias
  ("type ID = Int64" |> Builtin.parserParseToSimplifiedTree) = ParsedNode
    { typ = "source_file"
      fieldName = PACKAGE.Darklang.Stdlib.Option.Option.None
      text = "type ID = Int64"
      sourceRange = range (0L, 0L) (0L, 15L)
      children =
        [ ParsedNode
            { fieldName = PACKAGE.Darklang.Stdlib.Option.Option.None
              typ = "type_decl"
              text = "type ID = Int64"
              sourceRange = range (0L, 0L) (0L, 15L)
              children =
                [ ParsedNode
                    { fieldName =
                        PACKAGE.Darklang.Stdlib.Option.Option.Some "keyword_type"
                      typ = "keyword"
                      text = "type"
                      sourceRange = range (0L, 0L) (0L, 4L)
                      children = [] }

                  ParsedNode
                    { fieldName = PACKAGE.Darklang.Stdlib.Option.Option.Some "name"
                      typ = "type_identifier"
                      text = "ID"
                      sourceRange = range (0L, 5L) (0L, 7L)
                      children = [] }

                  ParsedNode
                    { fieldName =
                        PACKAGE.Darklang.Stdlib.Option.Option.Some "symbol_equals"
                      typ = "symbol"
                      text = "="
                      sourceRange = range (0L, 8L) (0L, 9L)
                      children = [] }

                  ParsedNode
                    { fieldName = PACKAGE.Darklang.Stdlib.Option.Option.Some "typ"
                      typ = "type_reference"
                      text = "Int64"
                      sourceRange = range (0L, 10L) (0L, 15L)
                      children =
                        [ ParsedNode
                            { fieldName = PACKAGE.Darklang.Stdlib.Option.Option.None
                              typ = "builtin_type"
                              text = "Int64"
                              sourceRange = range (0L, 10L) (0L, 15L)
                              children = [] } ] } ] } ] }


  ("" |> Builtin.parserParseToSimplifiedTree) = ParsedNode
    { typ = "source_file"
      fieldName = PACKAGE.Darklang.Stdlib.Option.Option.None
      text = ""
      sourceRange = range (0L, 0L) (0L, 0L)
      children = [] }

// These tests are a huge pain to write and maintain
// Let's focus on roundtripping tests, largely,
// and just have one of these for some base-line checking.
///
/// TODO: make that one test much more comprehensive
module ParseNodeToWrittenTypes =
  ("type MyID = Int64"
   |> PACKAGE.Darklang.LanguageTools.Parser.parseToSimplifiedTree
   |> PACKAGE.Darklang.LanguageTools.Parser.parseCliScript
   |> Builtin.unwrap) = PACKAGE
    .Darklang
    .LanguageTools
    .WrittenTypes
    .ParsedFile
    .CliScript(
      PACKAGE.Darklang.LanguageTools.WrittenTypes.CliScript
        { range = range (0L, 0L) (0L, 17L)
          typesAndFns =
            [ PACKAGE.Darklang.LanguageTools.WrittenTypes.CliScriptTypeOrFn.Type(
                (PACKAGE.Darklang.LanguageTools.WrittenTypes.TypeDeclaration.TypeDeclaration
                  { range = range (0L, 0L) (0L, 17L)
                    name =
                      PACKAGE.Darklang.LanguageTools.WrittenTypes.TypeIdentifier
                        { range = range (0L, 5L) (0L, 9L)
                          name = "MyID" }
                    definition =
                      PACKAGE
                        .Darklang
                        .LanguageTools
                        .WrittenTypes
                        .TypeDeclaration
                        .Definition
                        .Alias(
                          PACKAGE
                            .Darklang
                            .LanguageTools
                            .WrittenTypes
                            .TypeReference
                            .TypeReference
                            .Builtin(
                              PACKAGE
                                .Darklang
                                .LanguageTools
                                .WrittenTypes
                                .TypeReference
                                .Builtin
                                .TInt64(range (0L, 12L) (0L, 17L))
                            )
                        )
                    keywordType = range (0L, 0L) (0L, 4L)
                    symbolEquals = range (0L, 10L) (0L, 11L) })
              ) ]
          unparseableStuff = []
          exprsToEval = [] }
    )


module TextToTextRoundtripping =
  let prettyPrintCliScript
    (parsedFile: PACKAGE.Darklang.LanguageTools.WrittenTypes.ParsedFile)
    : String =
    let script =
      match parsedFile with
      | CliScript script -> script

    let typesAndFnsPart = // List<String>
      (PACKAGE.Darklang.Stdlib.List.fold script.typesAndFns [] (fun acc typeOrFn ->
        match typeOrFn with
        | Type typeDecl ->
          let prettyPrinted =
            (PACKAGE.Darklang.LanguageTools.NameResolver.empty ())
            |> PACKAGE.Darklang.LanguageTools.WrittenTypesToProgramTypes.TypeDeclaration.toUserTypePT
              typeDecl
            |> PACKAGE.Darklang.PrettyPrinter.ProgramTypes.userType

          acc |> PACKAGE.Darklang.Stdlib.List.push prettyPrinted

        | Function fnDecl ->
          let prettyPrinted =
            (PACKAGE.Darklang.LanguageTools.NameResolver.empty ())
            |> PACKAGE.Darklang.LanguageTools.WrittenTypesToProgramTypes.FunctionDeclaration.toUserFnPT
              fnDecl
            |> PACKAGE.Darklang.PrettyPrinter.ProgramTypes.userFunction

          acc |> PACKAGE.Darklang.Stdlib.List.push prettyPrinted))
      |> PACKAGE.Darklang.Stdlib.List.reverse

    let exprsPart = // List<String>
      (PACKAGE.Darklang.Stdlib.List.fold script.exprsToEval [] (fun acc expr ->
        let prettyPrinted =
          (PACKAGE.Darklang.LanguageTools.NameResolver.empty ())
          |> PACKAGE.Darklang.LanguageTools.WrittenTypesToProgramTypes.Expr.toPT
            expr
          |> PACKAGE.Darklang.PrettyPrinter.ProgramTypes.expr

        acc |> PACKAGE.Darklang.Stdlib.List.push prettyPrinted))
      |> PACKAGE.Darklang.Stdlib.List.reverse

    [ typesAndFnsPart; exprsPart ]
    |> PACKAGE.Darklang.Stdlib.List.flatten
    |> PACKAGE.Darklang.Stdlib.String.join "\n\n"


  let roundtripCliScript (text: String) : String =
    text
    |> PACKAGE.Darklang.LanguageTools.Parser.parseToSimplifiedTree
    |> PACKAGE.Darklang.LanguageTools.Parser.parseCliScript
    |> Builtin.unwrap
    |> prettyPrintCliScript


  module TypeReference =
    // all built-ins
    ("type MyUnit = Unit" |> roundtripCliScript) = "type MyUnit =\n  Unit"
    ("type MyBool = Bool" |> roundtripCliScript) = "type MyBool =\n  Bool"
    ("type MyInt64 = Int64" |> roundtripCliScript) = "type MyInt64 =\n  Int64"
    ("type MyFloat = Float" |> roundtripCliScript) = "type MyFloat =\n  Float"
    ("type MyChar = Char" |> roundtripCliScript) = "type MyChar =\n  Char"
    ("type MyString = String" |> roundtripCliScript) = "type MyString =\n  String"

    // single-part qualified name
    ("type ID = Test" |> roundtripCliScript) = "type ID =\n  Test"

    // fully-qualified package name (multi-part)
    ("type MyOption = PACKAGE.Darklang.Stdlib.Option.Option" |> roundtripCliScript) = "type MyOption =\n  PACKAGE.Darklang.Stdlib.Option.Option_v0"


  module TypeDeclaration =
    ("type SimpleAlias = Unit" |> roundtripCliScript) = "type SimpleAlias =\n  Unit"


  module Expr =
    // units
    ("()" |> roundtripCliScript) = "()"

    // bools
    ("true" |> roundtripCliScript) = "true"
    ("false" |> roundtripCliScript) = "false"

    // parens (disappear)
    ("(true)" |> roundtripCliScript) = "true"

    // int literals
    ("0L" |> roundtripCliScript) = "0L"
    ("1900L" |> roundtripCliScript) = "1900L"

    // string literals
    ("\"\"" |> roundtripCliScript) = "\"\""
    ("\"hello\"" |> roundtripCliScript) = "\"hello\""
    ("\"hello\\tworld\"" |> roundtripCliScript) = "\"hello\\tworld\""

    // variables and let bindings
    ("assumedlyAVariableName" |> roundtripCliScript) = "assumedlyAVariableName"
    // TODO: this is ugly
    ("let x = 1L\n  x" |> roundtripCliScript) = "let x =\n  1L\nx"

    // fn calls
    // TODO: these is ugly
    ("1L + 2L" |> roundtripCliScript) = "(1L) + (2L)"
    ("1L + b + 3L" |> roundtripCliScript) = "((1L) + (b)) + (3L)"
    ("(and true false)" |> roundtripCliScript) = "and true false"
    ("(Bool.and true false)" |> roundtripCliScript) = "Bool.and true false"
    ("(PACKAGE.Darklang.Stdlib.Bool.and true false)" |> roundtripCliScript) = "PACKAGE.Darklang.Stdlib.Bool.and_v0 true false"
    ("(Builtin.int64Add 1L 2L)" |> roundtripCliScript) = "Builtin.int64Add 1L 2L"

  module FunctionDeclaration =
    // single 'normal' param
    ("let helloWorld (i: Int64): String = \"Hello world\"" |> roundtripCliScript) = "let helloWorld (i: Int64): String =\n  \"Hello world\""

    ("let double2 (i: PACKAGE.Darklang.LanguageTools.ID_v0) : Int64 = i + i"
     |> roundtripCliScript) = "let double2 (i: PACKAGE.Darklang.LanguageTools.ID_v0): Int64 =\n  (i) + (i)"

    // () param
    ("let emptyString () : String = \"\"" |> roundtripCliScript) = "let emptyString (_: Unit): String =\n  \"\""


    // multiple params
    ("let isHigher (a: Int64) (b: Int64) : Bool = (PACKAGE.Darklang.Stdlib.Int64.greaterThan_v0 a b)"
     |> roundtripCliScript) = "let isHigher (a: Int64) (b: Int64): Bool =\n  PACKAGE.Darklang.Stdlib.Int64.greaterThan_v0 a b"



  module FnCalls =
    //package function call
    ("let sum (a : Int64) (b : Int64) : Int64 = (PACKAGE.Darklang.Stdlib.Int64.add a b)"
     |> roundtripCliScript) = "let sum (a: Int64) (b: Int64): Int64 =\n  PACKAGE.Darklang.Stdlib.Int64.add_v0 a b"



    module CliScript =
      // TODO the output here is a bit broken
      ("""
type BookID = Int64

let getTitle (bookId: BookID): String =
  let book = (Library.getBook bookId)
  (getNameFromBook book)

let curiousGeorgeBookId = 101L
(Builtin.printLine (getTitle curiousGeorgeBookId))

0L
"""
       |> roundtripCliScript) = """type BookID =
  Int64

let getTitle (bookId: BookID): String =
  let book =
    Library.getBook bookId
  getNameFromBook book

let curiousGeorgeBookId =
  101L
Builtin.printLine getTitle curiousGeorgeBookId

0L"""