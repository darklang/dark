// ---------------
// Misc
// ---------------
module Documentation =
  (Stdlib.List.length_v0 (Builtin.languageToolsAllBuiltinFns ()) > 100L) = true

  ((Builtin.languageToolsAllBuiltinFns ())
   |> Stdlib.List.findFirst (fun f -> f.name.name == "int64Add")
   |> Builtin.unwrap
   |> (fun f -> f.parameters)) =
     [ Darklang.LanguageTools.BuiltinFunctionParameter { name = "a"; ``type`` = Darklang.LanguageTools.RuntimeTypes.TypeReference.TInt64 }
       Darklang.LanguageTools.BuiltinFunctionParameter { name = "b"; ``type`` = Darklang.LanguageTools.RuntimeTypes.TypeReference.TInt64 } ]


module Infra =
  // correct number of tables
  Stdlib.Dict.size_v0 (Builtin.darkInternalInfraGetAndLogTableSizes_v0 ()) = 24L


  // server build hash
  (match Builtin.darkInternalInfraServerBuildHash_v0 () with
   // in local dev, the value is "dev"
   | "dev" -> true

   // in ci, "circleci"
   | "circleci" -> true

   // otherwise it's the first 7 chars of the git hash
   | hash -> (Stdlib.String.length hash) == 7L) = true


module Canvas =
  // Test creating a new canvas - just check it we can call `owner` on it
  (let owner = (Builtin.testGetCanvasID ()) |> Builtin.darkInternalCanvasOwner
   let newID = Builtin.darkInternalCanvasCreate owner "test"
   Builtin.darkInternalCanvasOwner newID == owner) = true


module Domains =
  module Roundtrip =
    ((Builtin.testGetCanvasID ())
     |> Builtin.darkInternalCanvasDomainGet
     |> Stdlib.List.head
     |> Builtin.unwrap
     |> Builtin.darkInternalCanvasDomainToCanvasID) =
      Stdlib.Result.Result.Ok(Builtin.testGetCanvasID ())

  module Errors =
    Builtin.darkInternalCanvasDomainGet (
      (Stdlib.Uuid.parse_v0 "7d9e5495-b068-4364-a2cc-3633ab4d13e6") |> Builtin.unwrap
    ) = []

    Builtin.darkInternalCanvasDomainToCanvasID "not-a-real-domain" =
      Stdlib.Result.Result.Error "Canvas not found"


module Toplevels =
  module WithDB =
    type X = { x: String }

    [<DB>]
    type TLXDB = X

    (Builtin.testGetCanvasID ())
    |> Builtin.darkInternalCanvasDBUnlocked
    |> Stdlib.List.length = 1L

  module NoDB =
    (Builtin.testGetCanvasID ()) |> Builtin.darkInternalCanvasDBUnlocked = []

    (Builtin.testGetCanvasID ())
    |> Builtin.darkInternalCanvasDeleteToplevelForever 1UL = false


module UnlockedDB =
  // none
  (Builtin.testGetCanvasID ()) |> Builtin.darkInternalCanvasDBUnlocked = []

  module WithDB =
    type X = { x: String }

    [<DB>]
    type UXDB = X
    // one
    (Builtin.testGetCanvasID ())
    |> Builtin.darkInternalCanvasDBUnlocked
    |> Stdlib.List.length = 1L

    // one but locked
    (let _ = Builtin.dbSet (X { x = "str" }) "test" UXDB
     (Builtin.testGetCanvasID ()) |> Builtin.darkInternalCanvasDBUnlocked) = []
